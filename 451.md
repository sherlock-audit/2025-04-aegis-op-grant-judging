Clean Aquamarine Haddock

High

# Incorrect Insurance Fee Accounting in approveRedeemRequest Leads to Collateral Over-Redemption

### Summary

In the `AegisMinting::approveRedeemRequest` function, which help to complete the redeem request of YUSD to collateral assets. But it is not implemented currently. Users are able to receive full collateral what it requested to redeemed without paying the fee. And the fees charged is actually on the total amount which supposed to be burned. Causing a mismatch between YUSD burned and collateral withdrawn. Over time, this leads to issue the asset backing the YUSD falls short.


### Root Cause

The root cause lies in [AegisMinting::approveRedeemRequest](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/4aceb235db96b2299bb95ebf16e83a24f987bf3e/aegis-contracts/contracts/AegisMinting.sol#L315) which will by called by the funds manager to approve redeem requests of users. According to the system there is insurance funds fee which will be charged on redemption on user which is not implemented correctly.

```solidity
@>    uint256 collateralAmount = _calculateRedeemMinCollateralAmount(request.order.collateralAsset, amount, request.order.yusdAmount);
    
    .....

    // Take a fee, if it's applicable
 @>   (uint256 burnAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(request.order.yusdAmount, redeemFeeBP);
    if (fee > 0) {
      yusd.safeTransfer(insuranceFundAddress, fee);
    }

    request.status = RedeemRequestStatus.APPROVED;
    totalRedeemLockedYUSD -= request.order.yusdAmount;

    IERC20(request.order.collateralAsset).safeTransfer(request.order.userWallet, collateralAmount);
 @>   yusd.burn(burnAmount);
```
Here we calculating the `collateralAmount` as per `request.order.yusdAmount` (total yusd amount to withdraw). It is calculated as per full yusd amount excluding fee. and later we are sending the `collateralAmount` to user which calculated for whole yusd amount. So the fee is not charged on the redeem amount.

The fee for insurance fund charged through `_calculateInsuranceFundFeeFromAmount` And we only burning the `burnAmount` which is (totalYusdAmount - isuranceFundFee) but the `collateralAmount` which is calculated as per `request.order.yusdAmount` the total amount is not burned what burned is `request.order.yusdAmount - fundFeeInsurance` 

So this `fee` is actually the amount which doesn't have any asset backing, the asset backing this amount are already withdrawal in `collateralAmount`, over time it will decrease the total asset backing for tokens, which lead the YUSD price decrease.
 

### Internal Pre-conditions

`  uint16 public redeemFeeBP`
should be set so the fees would be charged

### External Pre-conditions

There should be redeem request

### Attack Path

Works on every redeem request available.

### Impact

There are multiple impact it caused 
1) Fees is not charged from user for redeemRequest.
2) The fee charged is actually the token which supposed to be burned. So more collateral is redeemed then the actually burning amount. Over time, this drains the backing assets of the system, leading to a collateral shortfall.



### PoC

```solidity
@>    uint256 collateralAmount = _calculateRedeemMinCollateralAmount(request.order.collateralAsset, amount, request.order.yusdAmount);
    
    .....

    // Take a fee, if it's applicable
 @>   (uint256 burnAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(request.order.yusdAmount, redeemFeeBP);
    if (fee > 0) {
      yusd.safeTransfer(insuranceFundAddress, fee);
    }

    request.status = RedeemRequestStatus.APPROVED;
    totalRedeemLockedYUSD -= request.order.yusdAmount;

    IERC20(request.order.collateralAsset).safeTransfer(request.order.userWallet, collateralAmount);
 @>   yusd.burn(burnAmount);
```

Practical Example
1) User request 1000 YUSD to withdraw. (`request.order.yusdAmount` = 100 YUSD)
2) According to value collateralAmount is 100 USDC (`uint256 collateralAmount` = 100 USDC)
3) Let's suppose fee is 5%.
4) `burnAmount` = 95 YUSD and `fee` = 5 YUDS
4)That 100 USDC is sent to the user // Fee is n't charged to user.
`IERC20(request.order.collateralAsset).safeTransfer(request.order.userWallet, collateralAmount);`
5) The amount burned is 95 YUSD (`burnAmount`).  // Burn the partial amount then actual value.


I have used foundry for testing, check here for [integration](https://hardhat.org/hardhat-runner/docs/advanced/hardhat-and-foundry)
```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";
import {YUSD} from "contracts/YUSD.sol";
import {AegisMinting} from "contracts/AegisMinting.sol";
import {AegisConfig} from "contracts/AegisConfig.sol";
import {AegisOracle} from "contracts/AegisOracle.sol";
import {FeedRegistry} from "contracts/test/FeedRegistry.sol";
import {TestToken} from "contracts/test/TestToken.sol";

import {IAegisRewards} from "contracts/interfaces/IAegisRewards.sol";
import {IAegisConfig} from "contracts/interfaces/IAegisConfig.sol";
import {IAegisOracle} from "contracts/interfaces/IAegisOracle.sol";
import {IYUSD} from "contracts/interfaces/IYUSD.sol";
import {FeedRegistryInterface} from "@chainlink/contracts/src/v0.8/interfaces/FeedRegistryInterface.sol";
import {OrderLib} from  "contracts/lib/OrderLib.sol";

import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import "@openzeppelin/contracts/utils/cryptography/MessageHashUtils.sol";
import { Denominations } from "@chainlink/contracts/src/v0.8/Denominations.sol";

contract POC is Test {

    using ECDSA for bytes32;
    bytes32 internal constant ORDER_TYPE_HASH = keccak256(
        "Order(uint8 orderType,address userWallet,address collateralAsset,uint256 collateralAmount,uint256 yusdAmount,uint256 slippageAdjustedAmount,uint256 expiry,uint256 nonce,bytes additionalData)"
    );

    AegisMinting aeMint;
    IAegisConfig aegisConfig;
    AegisOracle aeOracle;
    YUSD yusd;
    TestToken mockERC20;
    FeedRegistry feedReg;

    address admin = makeAddr("admin");
    address oracle = makeAddr("oracle");
    address[] _assets;
    uint32[] heartbeat;
    address[] custodian;
    address[] ops;
    address _initialOwner = makeAddr("_initialOwner");
    address user = makeAddr("user");

    bytes32 private constant SETTINGS_MANAGER_ROLE = keccak256("SETTINGS_MANAGER_ROLE");
      bytes32 private constant FUNDS_MANAGER_ROLE = keccak256("FUNDS_MANAGER_ROLE");



    uint256 demoPrivateKey = 0x8db9a2e420ea6bf45ebd46411be83ae7f66cab825e04769f5c3a60b264d36c04;
    address trustedSigner = vm.addr(demoPrivateKey);


    function setUp() public {
        // Correctly push into dynamic arrays

        mockERC20 = new TestToken("MOCK USDC", "USDC", 6);
        _assets.push(address(1));
        _assets.push(address(mockERC20));

        ops.push(admin);

        heartbeat.push(5);
        heartbeat.push(0);

        // Optionally, if you want custodians as well, push addresses
        // custodian.push(address(4)); // Example if needed
        //   constructor(address _trustedSigner, address[] memory _ops, address _initialOwner) Ownable(_initialOwner) {
        
        yusd = new YUSD(admin);
        aeOracle = new AegisOracle(ops, _initialOwner);
        feedReg = new FeedRegistry();
        AegisConfig _aegisConfig = new AegisConfig(trustedSigner, ops, _initialOwner);


        IYUSD _yusd = IYUSD(address(yusd));
        aegisConfig = IAegisConfig(address(_aegisConfig));
        IAegisRewards _aegisRewards = IAegisRewards(address(2));
        IAegisOracle _aegisOracle = IAegisOracle(address(aeOracle));
        FeedRegistryInterface _fdRegistry = FeedRegistryInterface(address(feedReg));

        aeMint = new AegisMinting(
            _yusd,
            aegisConfig,
            _aegisRewards,
            _aegisOracle,
            _fdRegistry,
            address(0), 
            _assets,
            heartbeat,
            custodian,
            admin
        );

        // grant roles
        vm.startPrank(admin);
        aeMint.grantRole(SETTINGS_MANAGER_ROLE, admin);
        aeMint.grantRole(FUNDS_MANAGER_ROLE, admin);
        aeMint.setRedeemFeeBP(1000);

        _aegisConfig.whitelistAddress(user, true);
        vm.stopPrank();

        vm.prank(yusd.owner());
        yusd.setMinter(address(aeMint));

    }

    function testRedeemFeeHasNotCharged() public {


        // create order
         OrderLib.Order memory order = buildOrder(
            1, // Redeem
            user,
            address(mockERC20),
            100e6,
            100 ether,
            0,
            block.timestamp + 1 hours,
            1,
            abi.encode("Redeem Request 1")
        );

        
        bytes memory signature = signOrder(order, aeMint.getDomainSeparator(), demoPrivateKey);

        // Mock calls need
        // Mock the yusdUSDPrice() call to return 1e8 1 aUSD to 1 USD
        feedReg.setPrice(address(mockERC20), Denominations.USD, 1e8);
        vm.mockCall(
            address(aeOracle), 
            abi.encodeWithSelector(AegisOracle.yusdUSDPrice.selector),
            abi.encode(int256(1e8))
        );


        deal(address(yusd), user, 100 ether); // user has the YUSD worth 100 USDC
        vm.startPrank(user);
        yusd.approve(address(aeMint), yusd.balanceOf(user));
        aeMint.requestRedeem(order, signature);
        vm.stopPrank();

        // 2nd step approveRequestRedeem
        deal(address(mockERC20), address(aeMint), 100e6);
        vm.prank(admin);
        aeMint.approveRedeemRequest("Redeem Request 1", 100e6);
        uint bal = mockERC20.balanceOf(user);
        console.log("user Bal", bal); // USer able to withdraw the 100 USDC fee's hasn't charged

    }


    function buildOrder(
        uint8 orderType,
        address _user,
        address collateralAsset,
        uint256 collateralAmount,
        uint256 yusdAmount,
        uint256 slippageAdjustedAmount,
        uint256 expiry,
        uint256 nonce,
        bytes memory additionalData
    ) public pure returns (OrderLib.Order memory) {
        return OrderLib.Order({
            orderType: OrderLib.OrderType(orderType),
            userWallet: _user,
            collateralAsset: collateralAsset,
            collateralAmount: collateralAmount,
            yusdAmount: yusdAmount,
            slippageAdjustedAmount: slippageAdjustedAmount,
            expiry: expiry,
            nonce: nonce,
            additionalData: additionalData
        });
    }

    function signOrder(
        OrderLib.Order memory order,
        bytes32 domainSeparator,
        uint256 privateKey
    ) public pure returns (bytes memory) {
        bytes memory encoded = abi.encode(
            ORDER_TYPE_HASH,
            order.orderType,
            order.userWallet,
            order.collateralAsset,
            order.collateralAmount,
            order.yusdAmount,
            order.slippageAdjustedAmount,
            order.expiry,
            order.nonce,
            keccak256(order.additionalData)
        );

        bytes32 structHash = keccak256(encoded);
        bytes32 digest = MessageHashUtils.toTypedDataHash(domainSeparator, structHash);
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(privateKey, digest);
        return abi.encodePacked(r, s, v);
    }
}


```


### Mitigation

Two possible solution:
Calculate the collateral amount in `_calculateRedeemMinCollateralAmount` after charging the insurance fee.
Or charge insurance fee on collateralAmount.