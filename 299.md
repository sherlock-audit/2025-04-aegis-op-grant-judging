Dandy Laurel Lizard

High

# Malicious user can DoS whole period redeems when limit is in place

### Summary

Malicious user can DoS whole period redeems by only paying a few gas fees.

### Root Cause

The protocol uses a periods system where each period has a [limit](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/4aceb235db96b2299bb95ebf16e83a24f987bf3e/aegis-contracts/contracts/AegisMinting.sol#L785-L803) on the total amount of mints/redeems that can take place:

Everything is fine with mints, but the issue is that since the protocol uses a redeem requests/fulfill requests system for withdrawing assets, the `limits.currentPeriodTotalAmount` is increased on requests instead of upon fulfillment of those requests. What this allows is for a single malicious user to create a redeem request at the start of each period that fills the entire limit.

In cases like this, an access controlled function can be used to reject the request:

```solidity
  function _rejectRedeemRequest(string calldata requestId, RedeemRequest storage request) internal {
    request.status = RedeemRequestStatus.REJECTED;

    // Unlock YUSD
    totalRedeemLockedYUSD -= request.order.yusdAmount;
    yusd.safeTransfer(request.order.userWallet, request.order.yusdAmount);
  }
```

But although it decrements the `totalRedeemLockedYUSD` variable, it does not decrement the variable for the limit. Hence, even if an admin spots the malicious attempt to fill the limit and rejects it, the limit will still be filled and the user just receives back their tokens anyway.

Furthermore, the attacker can make a call to `withdrawRedeemRequest()` right after creating the request, and get their funds back instantly:

```solidity
  function withdrawRedeemRequest(string calldata requestId) public nonReentrant whenRedeemUnpaused {
    RedeemRequest storage request = _redeemRequests[keccak256(abi.encode(requestId))];
    if (request.timestamp == 0 || request.status != RedeemRequestStatus.PENDING || request.order.expiry > block.timestamp) {
      revert InvalidRedeemRequest();
    }

    request.status = RedeemRequestStatus.WITHDRAWN;

    // Unlock YUSD
    totalRedeemLockedYUSD -= request.order.yusdAmount;
    yusd.safeTransfer(request.order.userWallet, request.order.yusdAmount);
  }
```

Although the expiry of the request must have passed, it is an arbitrary variable set by the user upon redeem creation anyway, so it can be `block.timestamp` since the only check made on expiry is that it is not before `block.timestamp`:

```solidity
  function verify(
    Order calldata self,
    bytes32 domainSeparator,
    address expectedSigner,
    bytes calldata signature
  ) internal view returns (bytes32 orderHash) {
    orderHash = hashOrder(self, domainSeparator);
    address signer = ECDSA.recover(orderHash, signature);

    if (self.userWallet != msg.sender) revert InvalidSender();
    if (signer != expectedSigner) revert InvalidSignature();
    if (self.collateralAmount == 0) revert InvalidAmount();
    if (self.yusdAmount == 0) revert InvalidAmount();
@>  if (block.timestamp > self.expiry) revert SignatureExpired();
  }
```

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. Alice has a big holding of yUSD and since it's a stablecoin, it's safe to hold big amounts of it
2. New period starts and Alice calls `requestRedeem()` with `order.expiry == block.timestamp` (verify check passes for equal values)
3. Alice calls `withdrawRedeemRequest()`, the expiry is at `block.timestamp` so the check passes
4. Alice instantly receives back her funds, the redeem limits are filled and core protocol functionality to redeem assets is blocked for the whole period
5. Alice does not even pay a fee on the redeem, since the fee is only paid once a request is fulfilled, but if cancelled, no fee is paid

Alice can bundle the transactions to `requestRedeem(), withdrawRedeemRequest()` to instantly fill the limit and also get her funds back in a single block, **without** paying fees but only gas.

Core protocol functionality is disrupted meanwhile she only has to pay for gas and still has full access to her funds.

Since the attack is virtually no cost aside from gas and disrupts core functionality, I believe high severity is warranted. Furthermore, the admins specifically included that they want auditors to check:

>"Mint / Redeem:
• Check cap logic, rounding, and edge cases (e.g., tiny amounts, simultaneous mint and redeem).
• Verify collateral ratio can’t be bypassed."

And I believe this issue is quite severe and related to the cap logic.

### Impact

Core functionality is DoS'd and no redeems can take place

### PoC

_No response_

### Mitigation

Charging the fee upon redeem requests will easily combat this issue but it must be communicated to users if the fee will be refunded when withdrawing their requests or not.