Proper Taffy Mantaray

Medium

# More funds are frozen than expected.


### Summary

More funds are frozen than expected.

### Root Cause

There is a feature that the `FUNDS_MANAGER_ROLE` can freeze funds. The frozen funds must lie within the contract, and cannot be transferred to custody, not can it be used for user redeem or income deposit.

```solidity
  /// @dev Freeze asset funds and prevent them from transferring to custodians or users
  function freezeFunds(address asset, uint256 amount) external onlyRole(FUNDS_MANAGER_ROLE) onlySupportedAsset(asset) {
    if (assetFrozenFunds[asset] + amount > IERC20(asset).balanceOf(address(this))) {
      revert InvalidAmount();
    }

    assetFrozenFunds[asset] += amount;

    emit FreezeFunds(asset, amount);
  }
```

The issue here is, the actual frozen amount of funds is larger than `assetFrozenFunds[asset]`. Let's see an example, where `tokenBalance = 20, _custodyTransferrableAssetFunds[asset] = 10, assetFrozenFunds[asset] = 5`.

In this case, the contract has a total balance of 20, with 5 asset being frozen. The amount transferrable to custody is `_custodyAvailableAssetBalance(asset) = custodyTransferrableFunds[asset] - assetFrozenFunds[asset] = 10 - 5 = 5`. The amount can be used for user redeem is `balance - _custodyTransferrableAssetFunds[_asset] - assetFrozenFunds[_asset] = 20 - 10 - 5 = 5`.

Now, if we transfer 5 to custody, and allow user redeem 5, we will end up with `tokenBalance = 10, _custodyTransferrableAssetFunds[asset] = 5, assetFrozenFunds[asset] = 5`. At this state, both transferrable amount, and user redeemable amount would be 0. This means even though `assetFrozenFunds[asset]` is only 5, a total of 10 asset is locked up.

Ideally, we should only lock up 5 asset, and always allow transfer to custody have higher priority than user redeem. So in this case, the idea case should be we can still transfer 5 to custody, ending up with `tokenBalance = 5, _custodyTransferrableAssetFunds[asset] = 0, assetFrozenFunds[asset] = 5`.

https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L706

```solidity
  function transferToCustody(
    address wallet,
    address asset,
    uint256 amount
  ) external nonReentrant onlyRole(COLLATERAL_MANAGER_ROLE) onlySupportedAsset(asset) onlyCustodianAddress(wallet) {
    uint256 availableBalance = _custodyAvailableAssetBalance(asset);
    if (availableBalance < amount) {
      revert NotEnoughFunds();
    }

    _custodyTransferrableAssetFunds[asset] -= amount;
    IERC20(asset).safeTransfer(wallet, amount);

    emit CustodyTransfer(wallet, asset, amount);
  }

  function approveRedeemRequest(string calldata requestId, uint256 amount) external nonReentrant onlyRole(FUNDS_MANAGER_ROLE) whenRedeemUnpaused {
    ...
    uint256 availableAssetFunds = _untrackedAvailableAssetBalance(request.order.collateralAsset);
    if (availableAssetFunds < collateralAmount) {
      revert NotEnoughFunds();
    }
    ...
    IERC20(request.order.collateralAsset).safeTransfer(request.order.userWallet, collateralAmount);
  }

  function _custodyAvailableAssetBalance(address _asset) internal view returns (uint256) {
    uint256 custodyTransferrableFunds = _custodyTransferrableAssetFunds[_asset];
    uint256 balance = IERC20(_asset).balanceOf(address(this));
    if (balance < custodyTransferrableFunds || custodyTransferrableFunds < assetFrozenFunds[_asset]) {
      return 0;
    }

    return custodyTransferrableFunds - assetFrozenFunds[_asset];
  }

  function _untrackedAvailableAssetBalance(address _asset) internal view returns (uint256) {
    uint256 balance = IERC20(_asset).balanceOf(address(this));
    if (balance < _custodyTransferrableAssetFunds[_asset] + assetFrozenFunds[_asset]) {
      return 0;
    }

    return balance - _custodyTransferrableAssetFunds[_asset] - assetFrozenFunds[_asset];
  }
```

### Internal pre-conditions

Some funds are frozen.

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

More funds are frozen than expected.

### PoC

N/A

### Mitigation

Allow transfer to custody have higher priority than user redeem. In `_custodyAvailableAssetBalance()` function, calculate how much assets are frozen for user redeem, and subtract that from `assetFrozenFunds[asset]` first. Something like:

```solidity
  function _custodyAvailableAssetBalance(address _asset) internal view returns (uint256) {
    uint userFrozenFunds = min(balance - _custodyTransferrableAssetFunds[_asset], assetFrozenFunds[_asset]);
    uint custoryFrozenFunds = assetFrozenFunds - userFrozenFunds;
    return custodyTransferrableFunds - custoryFrozenFunds;
  }  
```
