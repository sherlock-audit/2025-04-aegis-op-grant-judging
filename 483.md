Odd Pastel Llama

High

# Redeeming YUSD for collateral when a redeem fee is apllicable will cause YUSD to depeg and brick last user from redeeming

### Summary

When a redeem is approved by the fund manager, the entire collateral amount backing the user's YUSD is returned to them. However, when a redeem fee is applicable, the entire YUSD amount being backed by the collateral is not burned. Instead, a percentage of the YUSD is sent to the insurance fund address as the redeem fee, and the remaining amount is burned. This can be seen [here](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L345C4-L355C27).

This causes the YUSD value to go down and depeg from 1 USD, since the YUSD amount in circulation is larger than the backing stablecoin amount locked in `AegisMinting` contract. Also, the last person to redeem cannot get back their collateral since it is not backed by any collateral. 

### Root Cause

The redeem fee is taken from the YUSD amount to burn rather than the user's collateral, causing the YUSD amount in circulation to go up when the backing collateral exits the protocol (is released to the user).

### Internal Pre-conditions

A redeem fee is applicable. 

### External Pre-conditions

N/A.

### Attack Path

1. Users mint YUSD by providing stablecoins as collateral.
2. Settings manager sets the redeem fee to a non-zero value.
3. Users request to redeem their YUSD for the underlying collateral.
4. The redeem request is approved. A percentage of the YUSD is sent to the insurance fund address, while all the backing collateral amount is returned to the users.

### Impact

1. The YUSD price depegs from the expected 1 USD price.
2. The last person to redeem cannot get back their deposited collateral as the YUSD they hold is not backed by any collateral. If the insurance fund address redeems before other users, the last user is bricked from redeeming.

### PoC

Note, to make this PoC work, the following changes were made in the `OrderLib` library,

```diff
library OrderLib {
    // More code here...

    /// @dev Hashes order struct
-   function hashOrder(Order calldata order, bytes32 domainSeparator) internal pure returns (bytes32) {
+   function hashOrder(Order memory order, bytes32 domainSeparator) internal pure returns (bytes32) {
        return MessageHashUtils.toTypedDataHash(domainSeparator, keccak256(encodeOrder(order)));
    }

-   function encodeOrder(Order calldata order) internal pure returns (bytes memory) {
+   function encodeOrder(Order memory order) internal pure returns (bytes memory) {
        return abi.encode(
            ORDER_TYPE,
            order.orderType,
            order.userWallet,
            order.collateralAsset,
            order.collateralAmount,
            order.yusdAmount,
            order.slippageAdjustedAmount,
            order.expiry,
            order.nonce,
            keccak256(order.additionalData)
        );
    }

    // More code here...
}
```

Setup a foundry project, add all the contracts to `src`, and add `openzeppelin/openzeppelin-contracts` and `smartcontractkit/chainlink` as git submodules.
Create a `PoCs.t.sol` file, and add the following code to it,

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.25;

import {Test, console} from "forge-std/Test.sol";

import {IYUSD} from "../src/interfaces/IYUSD.sol";
import {IAegisConfig} from "../src/interfaces/IAegisConfig.sol";
import {IAegisRewards} from "../src/interfaces/IAegisRewards.sol";
import {IAegisOracle} from "../src/interfaces/IAegisOracle.sol";
import {FeedRegistryInterface} from "@chainlink/contracts/src/v0.8/interfaces/FeedRegistryInterface.sol";

import {AegisConfig} from "../src/AegisConfig.sol";
import {AegisMinting} from "../src/AegisMinting.sol";
import {AegisOracle} from "../src/AegisOracle.sol";
import {AegisRewards} from "../src/AegisRewards.sol";
import {YUSD} from "../src/YUSD.sol";
import {TestToken} from "../src/test/TestToken.sol";
import {FeedRegistry} from "../src/test/FeedRegistry.sol";
import {Denominations} from "@chainlink/contracts/src/v0.8/Denominations.sol";
import {OrderLib} from "../src/lib/OrderLib.sol";

contract PoCs is Test {
    bytes32 private constant SETTINGS_MANAGER_ROLE = keccak256("SETTINGS_MANAGER_ROLE");
    bytes32 private constant FUNDS_MANAGER_ROLE = keccak256("FUNDS_MANAGER_ROLE");
    bytes32 private constant COLLATERAL_MANAGER_ROLE = keccak256("COLLATERAL_MANAGER_ROLE");

    address private admin;
    address private trustedSigner;
    uint256 private trustedSignerPrivateKey;
    address private operator1;
    address private custodian;
    address private insuranceFundAddress;
    address private user1;
    address private user2;

    TestToken private usdc;
    TestToken private usdt;
    FeedRegistry private feedRegistry;

    AegisConfig private config;
    AegisOracle private oracle;
    AegisRewards private rewards;
    AegisMinting private minting;
    YUSD private yusd;

    function setUp() public {
        // Setup actors
        admin = makeAddr("admin");
        (trustedSigner, trustedSignerPrivateKey) = makeAddrAndKey("trusted-signer");
        operator1 = makeAddr("operator1");
        custodian = makeAddr("custodian");
        insuranceFundAddress = makeAddr("insurance-fund");
        user1 = makeAddr("user1");
        user2 = makeAddr("user2");

        // mock tokens and chainlink feed registry
        usdc = new TestToken("USD Coin", "USDC", 6);
        usdt = new TestToken("Tether USD", "USDT", 6);
        feedRegistry = new FeedRegistry();

        // Set prices of USDC and USDT to $1
        int256 usdPrice = int256(1 * 10 ** feedRegistry.decimals(address(usdc), Denominations.USD));
        feedRegistry.setPrice(address(usdc), Denominations.USD, usdPrice);
        feedRegistry.setPrice(address(usdt), Denominations.USD, usdPrice);

        // Sample constructor args
        address[] memory operators = new address[](1);
        operators[0] = operator1;
        address[] memory assets = new address[](2);
        assets[0] = address(usdc);
        assets[1] = address(usdt);
        uint32[] memory chainlinkAssetHeartbeats = new uint32[](2);
        chainlinkAssetHeartbeats[0] = 1 days;
        chainlinkAssetHeartbeats[1] = 1 days;
        address[] memory custodians = new address[](1);
        custodians[0] = custodian;

        // Setup contracts
        config = new AegisConfig(trustedSigner, operators, admin);
        oracle = new AegisOracle(operators, admin);
        yusd = new YUSD(admin);
        rewards = new AegisRewards(IYUSD(address(yusd)), IAegisConfig(address(config)), admin);
        minting = new AegisMinting(
            IYUSD(address(yusd)),
            IAegisConfig(address(config)),
            IAegisRewards(address(rewards)),
            IAegisOracle(address(oracle)),
            FeedRegistryInterface(address(feedRegistry)),
            insuranceFundAddress,
            assets,
            chainlinkAssetHeartbeats,
            custodians,
            admin
        );

        // Configure contracts
        vm.startPrank(admin);
        rewards.setAegisMintingAddress(address(minting));
        config.disableWhitelist(); // disable whitelist to allow anyone to mint YUSD
        yusd.setMinter(address(minting));
        minting.grantRole(SETTINGS_MANAGER_ROLE, admin); // Allow the admin to tweak the contract settings
        minting.grantRole(FUNDS_MANAGER_ROLE, admin); // Allow the admin to manage funds
        minting.grantRole(COLLATERAL_MANAGER_ROLE, admin); // Allow the admin to manage collateral assets
        vm.stopPrank();

        // Skip by a reasonable duration to avoid underfolows on price stalesness calculation
        uint256 duration = 10 days;
        skip(duration);
    }

    function test_redeemingWhenRedeemFeeIsApplicableCanCauseYUSDDepeg() external {
        // User1 mints 100k YUSD providing 100k USDC as collateral
        // User2 also mints 100k YUSD providing 100k USDC as collateral
        // When user1 redeems, 10% (redeem fee) of 100k YUSD (10k YUSD) is sent to the insurance fund, and 100k
        // USDC is returned to the user
        // The 10k YUSD in circulation is backed by nothing
        // Total YUSD amount in circulation: 110k YUSD
        // Total backing USDC collateral: 100k USDC
        // YUSD price: 100k / 110k = 0.909$ (YUSD depeg)

        uint256 mintAmountWithoutDecimals = 100_000;

        _mint(user1, usdc, mintAmountWithoutDecimals, 1);
        _mint(user2, usdc, mintAmountWithoutDecimals, 1);

        uint16 redeemFeeBP = 1_000; // 10% redeem fee (reasonable, as the max redeem fee that can be set is 50%)
        vm.prank(admin);
        minting.setRedeemFeeBP(redeemFeeBP);

        string memory requestId = "redeem#1";
        _redeem(user1, usdc, mintAmountWithoutDecimals, 1, requestId);

        uint256 backingColllateralScaled = usdc.balanceOf(address(minting)) * 10 ** (yusd.decimals() - usdc.decimals());
        uint256 yusdInCirculation = yusd.totalSupply();
        uint256 yusdUsdPriceScaled = (backingColllateralScaled * 1e18) / yusdInCirculation;

        console.logString("Backing USDC collateral (18 decimals): ");
        console.log(backingColllateralScaled);
        console.logString("YUSD in circulation (18 decimals): ");
        console.log(yusdInCirculation);
        console.logString("YUSD price in USD (scaled by 18 decimals): ");
        console.log(yusdUsdPriceScaled);
        assertGt(yusdInCirculation, backingColllateralScaled);
    }

    function _mint(address _user, TestToken _stablecoin, uint256 _mintAmount, uint256 _nonce) internal {
        _stablecoin.mint(_user, _mintAmount * 10 ** _stablecoin.decimals());

        OrderLib.Order memory mintOrder = OrderLib.Order({
            orderType: OrderLib.OrderType.MINT,
            userWallet: _user,
            collateralAsset: address(_stablecoin),
            collateralAmount: _mintAmount * 10 ** _stablecoin.decimals(),
            yusdAmount: _mintAmount * 10 ** yusd.decimals(),
            slippageAdjustedAmount: 0,
            expiry: block.timestamp + 1 hours,
            nonce: _nonce,
            additionalData: ""
        });
        bytes32 mintOrderHash = OrderLib.hashOrder(mintOrder, minting.getDomainSeparator());
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(trustedSignerPrivateKey, mintOrderHash);
        bytes memory mintSignature = abi.encodePacked(r, s, v);

        vm.startPrank(_user);
        usdc.approve(address(minting), _mintAmount * 10 ** _stablecoin.decimals());
        minting.mint(mintOrder, mintSignature);
        vm.stopPrank();
    }

    function _redeem(address _user, TestToken _stablecoin, uint256 _amount, uint256 _nonce, string memory _requestId)
        internal
    {
        OrderLib.Order memory redeemOrder = OrderLib.Order({
            orderType: OrderLib.OrderType.REDEEM,
            userWallet: _user,
            collateralAsset: address(_stablecoin),
            collateralAmount: _amount * 10 ** _stablecoin.decimals(),
            yusdAmount: _amount * 10 ** yusd.decimals(),
            slippageAdjustedAmount: 0,
            expiry: block.timestamp + 1 hours,
            nonce: _nonce,
            additionalData: abi.encode(_requestId)
        });
        bytes32 redeemOrderHash = OrderLib.hashOrder(redeemOrder, minting.getDomainSeparator());
        (uint8 v2, bytes32 r2, bytes32 s2) = vm.sign(trustedSignerPrivateKey, redeemOrderHash);
        bytes memory redeemSignature = abi.encodePacked(r2, s2, v2);

        vm.startPrank(user1);
        yusd.approve(address(minting), _amount * 10 ** yusd.decimals());
        minting.requestRedeem(redeemOrder, redeemSignature);
        vm.stopPrank();

        vm.startPrank(admin);
        minting.transferToCustody(custodian, address(usdc), _amount * 10 ** _stablecoin.decimals()); // Transfer to custody
        vm.stopPrank();

        vm.startPrank(custodian);
        _stablecoin.transfer(address(minting), _amount * 10 ** _stablecoin.decimals()); // Release from custody
        vm.stopPrank();

        vm.startPrank(admin);
        minting.approveRedeemRequest(_requestId, _amount * 10 ** _stablecoin.decimals());
        vm.stopPrank();
    }
}
```

Now run the test with `forge test --mt test_redeemingWhenRedeemFeeIsApplicableCanCauseYUSDDepeg -vvv`.

Logs,

```shell
[â ’] Compiling...
No files changed, compilation skipped

Ran 1 test for test/PoCs.sol:PoCs
[PASS] test_redeemingWhenRedeemFeeIsApplicableCanCauseYUSDDepeg() (gas: 771428)
Logs:
  Backing USDC collateral (18 decimals): 
  100000000000000000000000
  YUSD in circulation (18 decimals): 
  110000000000000000000000
  YUSD price in USD (scaled by 18 decimals): 
  909090909090909090

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 14.54ms (6.73ms CPU time)

Ran 1 test suite in 17.29ms (14.54ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

Burn the entire YUSD amount, and charge a redeem fee on the collateral to be released.