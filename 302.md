Mean Laurel Barracuda

Medium

# Incorrect minting limit check in the `mint()` function

### Summary
Whitelisted users can mint YUSD stablecoins by providing the supported collateral assets through the `mint()` function. Since there is a limit on the amount of YUSD that can be minted during the current period, the `mint()` function checks to ensure that the total amount of minted YUSD does not exceed this limit. However, the current implementation does not account for the newly minted YUSD amount in this check; instead, it uses the amount specified by the user as a parameter. Consequently, this oversight may lead to situations where legitimate users experience a DoS for this function.

### Root Cause
In the `mint()` function, at [L250](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/4aceb235db96b2299bb95ebf16e83a24f987bf3e/aegis-contracts/contracts/AegisMinting.sol#L250), there is a check to ensure that the total amount of minted YUSD does not exceed the predefined limit. 
Currently, it uses `order.yusdAmount` as the newly minted amount of YUSD specified by the user as a parameter. However, as shown in the below `_calculateMinYUSDAmount` function, the actual newly minted amount of YUSD is represented by `yusdAmount`, which may differ from `order.yusdAmount`.
```solidity
    function mint(
        OrderLib.Order calldata order,
        bytes calldata signature
    ) external nonReentrant onlyWhitelisted(order.userWallet) onlySupportedAsset(order.collateralAsset) {
        [...]

@>      _checkMintRedeemLimit(mintLimit, order.yusdAmount);
        order.verify(getDomainSeparator(), aegisConfig.trustedSigner(), signature);
        _deduplicateOrder(order.userWallet, order.nonce);

@>      uint256 yusdAmount = _calculateMinYUSDAmount(order.collateralAsset, order.collateralAmount, order.yusdAmount);
        if (yusdAmount < order.slippageAdjustedAmount) {
        revert PriceSlippage();
        }

        // Take a fee, if it's applicable
        (uint256 mintAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(yusdAmount, mintFeeBP);
        if (fee > 0) {
@>      yusd.mint(insuranceFundAddress, fee);
        }

        IERC20(order.collateralAsset).safeTransferFrom(order.userWallet, address(this), order.collateralAmount);
@>      yusd.mint(order.userWallet, mintAmount);
        _custodyTransferrableAssetFunds[order.collateralAsset] += order.collateralAmount;

        emit Mint(_msgSender(), order.collateralAsset, order.collateralAmount, mintAmount, fee);
    }
```
```solidity
    function _calculateMinYUSDAmount(address collateralAsset, uint256 collateralAmount, uint256 yusdAmount) internal view returns (uint256) {
        (uint256 chainlinkPrice, uint8 feedDecimals) = _getAssetUSDPriceChainlink(collateralAsset);
        if (chainlinkPrice == 0) {
        return yusdAmount;
        }

        uint256 chainlinkYUSDAmount = Math.mulDiv(
        collateralAmount * 10 ** (18 - IERC20Metadata(collateralAsset).decimals()),
        chainlinkPrice,
        10 ** feedDecimals
        );

        // Return smallest amount
@>      return Math.min(yusdAmount, chainlinkYUSDAmount);
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
The amount of YUSD calculated from the value of the collateral, based on Chainlink prices, should be less than the amount specified by the user in the order.

### Impact
Legitimate users may experience a DoS with the `mint()` function. Since the `mint()` function is essential for receiving collateral tokens to maintain a delta-neutral position, this vulnerability could lead to potential losses for the protocol.

### Mitigation
It is recommended to perform the minting limit check after calculating the actual amount of YUSD to be minted.
```diff
-   _checkMintRedeemLimit(mintLimit, order.yusdAmount);
    order.verify(getDomainSeparator(), aegisConfig.trustedSigner(), signature);
    _deduplicateOrder(order.userWallet, order.nonce);

    uint256 yusdAmount = _calculateMinYUSDAmount(order.collateralAsset, order.collateralAmount, order.yusdAmount);
+   _checkMintRedeemLimit(mintLimit, yusdAmount);    
```