Fantastic Silver Ostrich

High

# Double Count of Frozen Funds in `_untrackedAvailableAssetBalance` Causes Incorrect Balance Calculation

### Summary

The [`_untrackedAvailableAssetBalance`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/4aceb235db96b2299bb95ebf16e83a24f987bf3e/aegis-contracts/contracts/AegisMinting.sol#L716-L723) function in `AegisMinting.sol`, which is used in both `approveRedeemRequest` and `depositIncome`, miscalculates available asset funds, leading to failures when users attempt to redeem or claim funds or when funds manager attempts to deposit income. The issue stems from the function checking the sum of `_custodyTransferrableAssetFunds` and `assetFrozenFunds` in `if` statement or subtracting both `_custodyTransferrableAssetFunds` and `assetFrozenFunds` from the contractâ€™s balance. 
The problem is, that `_custodyTransferrableAssetFunds` already includes frozen funds (as it is not adjusted when assets are frozen), the frozen amount is effectively subtracted/added twice. This double-counting causes the available balance to be understated, resulting in returning 0 or amount less, than it is supposed to be, causing valid requests being incorrectly rejected.
```solidity
   function _untrackedAvailableAssetBalance(address _asset) internal view returns (uint256) {
        uint256 balance = IERC20(_asset).balanceOf(address(this));
        if (balance < _custodyTransferrableAssetFunds[_asset] + assetFrozenFunds[_asset]) {
            return 0;
        }

        return balance - _custodyTransferrableAssetFunds[_asset] - assetFrozenFunds[_asset];
    }
```


### Root Cause

The [`_untrackedAvailableAssetBalance`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/4aceb235db96b2299bb95ebf16e83a24f987bf3e/aegis-contracts/contracts/AegisMinting.sol#L716-L723) function miscalculates available asset funds

### Internal Pre-conditions

1. there are frozen funds 

### External Pre-conditions

///

### Attack Path

This attack path is coded in PoC:
1. Bob mints YUSD with 10000 USDC
2. Bob's funds are transferred to custodian 
3. Alice mints YUSD with 10000 USDC
4. Alice is a criminal and her funds are frozen (accroding to comments: Freeze asset funds and prevent them from transferring to custodians or users)
5. Bob request to redeem his funds 
6. Redeem is valid, admin wants to approve his request
7. Custodian transfers funds to the contract, but `approveRedeemRequest` fails because of incorrect calculations in `_untrackedAvailableAssetBalance`, which determines that there are no available asset funds, even though the custodian previously transferred assets to the contract

### Impact

Users are not able to redeem their funds

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.26;

import {Test, console} from "forge-std/Test.sol";
import {AegisConfig} from "../src/AegisConfig.sol";
import {AegisMinting} from "../src/AegisMinting.sol";
import {AegisOracle} from "../src/AegisOracle.sol";
import {AegisRewards} from "../src/AegisRewards.sol";
import {YUSD} from "../src/YUSD.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {AggregatorV2V3Interface} from "@chainlink/contracts/src/v0.8/interfaces/AggregatorV2V3Interface.sol";
import {FeedRegistryInterface} from "@chainlink/contracts/src/v0.8/interfaces/FeedRegistryInterface.sol";
import {IAegisConfig} from "../src/interfaces/IAegisConfig.sol";
import {IAegisOracle} from "../src/interfaces/IAegisOracle.sol";
import {IAegisRewards} from "../src/interfaces/IAegisRewards.sol";
import {IYUSD} from "../src/interfaces/IYUSD.sol";
import {OrderLib} from "../src/lib/OrderLib.sol";
import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import {IAegisMintingErrors} from "../src/interfaces/IAegisMinting.sol";

contract MockERC20 is ERC20 {
    constructor(string memory name, string memory symbol) ERC20(name, symbol) {}

    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }

    function decimals() public view virtual override returns (uint8) {
        return 6;
    }
}

contract MockFeedRegistry {
    address public feed;

    function getFeed(address base, address quote) external view returns (AggregatorV2V3Interface aggregator) {
        return AggregatorV2V3Interface(feed);
    }

    function latestRoundData(address base, address quote)
        external
        view
        returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound)
    {
        return (0, 1e8, 0, block.timestamp - 10, 0);
    }

    function decimals(address base, address quote) external view returns (uint8) {
        return 8;
    }
}

contract SomeTest2 is Test {
    using OrderLib for OrderLib.Order;

    address public owner = makeAddr("owner");
    address public bob = makeAddr("bob");
    address public alice = makeAddr("alice");
    address public operator = makeAddr("operator");
    address public insuranceFund = makeAddr("insuranceFund");
    address public custodian = makeAddr("custodian");
    address public settingsManager = makeAddr("settingsManager");
    address public fundsManager = makeAddr("fundsManager");
    address public collateralManager = makeAddr("collateralManager");

    address public signer = vm.addr(0x5656);

    AegisConfig public aegisConfig;
    AegisMinting public aegisMinting;
    AegisOracle public aegisOracle;
    AegisRewards public aegisRewards;
    YUSD public yusd;
    MockERC20 public usdc;
    MockFeedRegistry public feedRegistry;

    bytes32 public constant ORDER_TYPE = keccak256(
        "Order(uint8 orderType,address userWallet,address collateralAsset,uint256 collateralAmount,uint256 yusdAmount,uint256 slippageAdjustedAmount,uint256 expiry,uint256 nonce,bytes additionalData)"
    );

    function setUp() public {
        vm.startPrank(owner);
        address[] memory operators = new address[](1);
        operators[0] = operator;
        aegisConfig = new AegisConfig(signer, operators, owner);
        aegisOracle = new AegisOracle(operators, owner);
        yusd = new YUSD(owner);
        usdc = new MockERC20("USDC", "USDC");
        aegisRewards = new AegisRewards(IYUSD(address(yusd)), IAegisConfig(address(aegisConfig)), owner);
        feedRegistry = new MockFeedRegistry();
        address[] memory assets = new address[](1);
        assets[0] = address(usdc);
        uint32[] memory heartbeats = new uint32[](1);
        heartbeats[0] = 60;
        address[] memory custodians = new address[](1);
        custodians[0] = custodian;

        aegisMinting = new AegisMinting(
            IYUSD(address(yusd)),
            IAegisConfig(address(aegisConfig)),
            IAegisRewards(address(aegisRewards)),
            IAegisOracle(address(aegisOracle)),
            FeedRegistryInterface(address(feedRegistry)),
            insuranceFund,
            assets,
            heartbeats,
            custodians,
            owner
        );

        yusd.setMinter(address(aegisMinting));

        aegisMinting.grantRole(keccak256("SETTINGS_MANAGER_ROLE"), settingsManager);
        aegisMinting.grantRole(keccak256("FUNDS_MANAGER_ROLE"), fundsManager);
        aegisMinting.grantRole(keccak256("COLLATERAL_MANAGER_ROLE"), collateralManager);

        vm.stopPrank();
        vm.startPrank(settingsManager);
        aegisMinting.setMintLimits(60, 1000000e18);
        aegisMinting.setRedeemLimits(60, 1000000e18);
        aegisMinting.setMintFeeBP(1000);
        aegisMinting.setRedeemFeeBP(1000);
        vm.stopPrank();
        vm.startPrank(operator);
        aegisConfig.whitelistAddress(bob, true);
        aegisConfig.whitelistAddress(alice, true);
    }

    function test__someTest212() public {
        vm.warp(block.timestamp + 1 days);
        // bob mints YUSD for 10000 USDC
        vm.startPrank(bob);
        usdc.mint(bob, 10000e6);
        usdc.approve(address(aegisMinting), type(uint256).max);
        OrderLib.Order memory order1 = OrderLib.Order({
            orderType: OrderLib.OrderType.MINT,
            userWallet: bob,
            collateralAsset: address(usdc),
            collateralAmount: 10000e6,
            yusdAmount: 10000e18,
            slippageAdjustedAmount: 9990e18,
            expiry: block.timestamp + 1 days,
            nonce: 1,
            additionalData: ""
        });
        aegisMinting.mint(order1, signerSign("AegisMinting", order1));
        vm.stopPrank();
        // bob's funds are transferred to custody
        vm.startPrank(collateralManager);
        aegisMinting.transferToCustody(custodian, address(usdc), 10000e6);
        vm.stopPrank();

        // alice mint YUSD for 100000 USDC
        vm.startPrank(alice);
        usdc.mint(alice, 10000e6);
        usdc.approve(address(aegisMinting), type(uint256).max);
        OrderLib.Order memory order2 = OrderLib.Order({
            orderType: OrderLib.OrderType.MINT,
            userWallet: alice,
            collateralAsset: address(usdc),
            collateralAmount: 10000e6,
            yusdAmount: 10000e18,
            slippageAdjustedAmount: 9990e18,
            expiry: block.timestamp + 1 days,
            nonce: 1,
            additionalData: ""
        });
        aegisMinting.mint(order2, signerSign("AegisMinting", order2));
        vm.stopPrank();

        // alices funds are frozen
        vm.startPrank(fundsManager);
        aegisMinting.freezeFunds(address(usdc), 10000e6);
        vm.stopPrank();

        // bob requests redeem of his funds

        vm.startPrank(bob);
        yusd.approve(address(aegisMinting), type(uint256).max);
        OrderLib.Order memory order3 = OrderLib.Order({
            orderType: OrderLib.OrderType.REDEEM,
            userWallet: bob,
            collateralAsset: address(usdc),
            collateralAmount: 10000e6,
            yusdAmount: 9000e18,
            slippageAdjustedAmount: 8999e6,
            expiry: block.timestamp + 1 days,
            nonce: 1,
            additionalData: abi.encode("request1")
        });
        aegisMinting.requestRedeem(order3, signerSign("AegisMinting", order3));
        vm.stopPrank();

        // admin approves, funds are being sent from custodian to the contract by some backend script
        vm.startPrank(custodian);
        usdc.transfer(address(aegisMinting), 10000e6);
        vm.stopPrank();

        // admin approves redeem request, but it reverts, with NotEnoughFunds error
        vm.startPrank(fundsManager);
        vm.expectRevert(IAegisMintingErrors.NotEnoughFunds.selector);
        aegisMinting.approveRedeemRequest("request1", 10000e6);
        vm.stopPrank();
    }

    function signerSign(string memory contractName, OrderLib.Order memory order)
        public
        returns (bytes memory signature)
    {
        bytes32 domainSeparator;
        if (keccak256(bytes(contractName)) == keccak256("AegisMinting")) {
            domainSeparator = aegisMinting.getDomainSeparator();
        } else if (keccak256(bytes(contractName)) == keccak256("AegisRewards")) {
            domainSeparator = aegisRewards.getDomainSeparator();
        } else {
            revert("Invalid contract name");
        }
        bytes32 structHash = keccak256(
            abi.encode(
                ORDER_TYPE,
                order.orderType,
                order.userWallet,
                order.collateralAsset,
                order.collateralAmount,
                order.yusdAmount,
                order.slippageAdjustedAmount,
                order.expiry,
                order.nonce,
                keccak256(order.additionalData)
            )
        );
        bytes32 digest = keccak256(abi.encodePacked("\x19\x01", domainSeparator, structHash));
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(0x5656, digest);
        signature = abi.encodePacked(r, s, v);
    }
}
```

### Mitigation

modify `_untrackedAvailableAssetBalance` to not double count frozen assets