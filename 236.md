Soaring Lemon Finch

Medium

# Both claimer and reward manager can claim reward at the same time

### Summary
Aegis can generate profits and distribute yield to designated YUSD holders.

The collateral yield will be deposited by calling [`AegisMinting#depositIncome()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L397-L410), then equal value of YUSD reward will be minted to `AegisRewards` contract. [`AegisRewards#depositRewards()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisRewards.sol#L158-L165) will be called to increase the reward amount for a specified requestId:

Once the reward request is ready to be claimed, reward manager will call [`finalizeRewards()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisRewards.sol#L131-L142) to update its status and set the expiry time.
```solidity
  function finalizeRewards(bytes32 id, uint256 claimDuration) external onlyRole(REWARDS_MANAGER_ROLE) {
    if (_rewards[id].finalized) {
      revert UnknownRewards();
    }

    _rewards[id].finalized = true;
    if (claimDuration > 0) {
      _rewards[id].expiry = block.timestamp + claimDuration;
    }

    emit FinalizeRewards(id, _rewards[id].expiry);
  }
```
Then the eligible user can claim the YUSD reward by calling [`claimRewards()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisRewards.sol#L91-L127) as far as the request is not expired:
```solidity
  function claimRewards(ClaimRewardsLib.ClaimRequest calldata claimRequest, bytes calldata signature) external nonReentrant {
    claimRequest.verify(getDomainSeparator(), aegisConfig.trustedSigner(), signature);

    uint256 count = 0;
    uint256 totalAmount = 0;
    bytes32[] memory claimedIds = new bytes32[](claimRequest.ids.length);
    uint256 len = claimRequest.ids.length;
    for (uint256 i = 0; i < len; i++) {
      if (
        !_rewards[claimRequest.ids[i]].finalized ||
        _rewards[claimRequest.ids[i]].amount == 0 ||
@>      (_rewards[claimRequest.ids[i]].expiry > 0 && _rewards[claimRequest.ids[i]].expiry < block.timestamp) ||
        _addressClaimedRewards[_msgSender()][claimRequest.ids[i]]
      ) {
        continue;
      }

      _addressClaimedRewards[_msgSender()][claimRequest.ids[i]] = true;
      _rewards[claimRequest.ids[i]].amount -= claimRequest.amounts[i];
      totalAmount += claimRequest.amounts[i];
      claimedIds[count] = claimRequest.ids[i];
      count++;
    }

    if (totalAmount == 0) {
      revert ZeroRewards();
    }

    yusd.safeTransfer(_msgSender(), totalAmount);

    /// @solidity memory-safe-assembly
    assembly {
      mstore(claimedIds, count)
    }

    emit ClaimRewards(_msgSender(), claimedIds, totalAmount);
  }
```
  Once the reward request is expired and there are some reward unclaimed, the reward manager can withdraw it by calling [`withdrawExpiredRewards()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisRewards.sol#L145-L155):
```solidity
  function withdrawExpiredRewards(bytes32 id, address to) external onlyRole(REWARDS_MANAGER_ROLE) {
@>  if (!_rewards[id].finalized || _rewards[id].amount == 0 || _rewards[id].expiry == 0 || _rewards[id].expiry > block.timestamp) {
      revert UnknownRewards();
    }

    uint256 amount = _rewards[id].amount;
    _rewards[id].amount = 0;
    yusd.safeTransfer(to, amount);

    emit WithdrawExpiredRewards(id, to, amount);
  }
```

However, when `block.timestamp` equals ` _rewards[id].expiry`, both `claimRewards()` and `withdrawExpiredRewards()` can be used to claim reward,  and this could cause a confliction.  Either the eligible claimer loss their valid rewards or the claimer could claim expired rewards.

### Root Cause

`_rewards[id].expiry` was mistakenly handled in edge case.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact
Either the valid reward could be withdrawn mistakenly by fund manager or the claimer could claim expired rewards

### PoC

### Mitigation
Edge case should be handled properly:
```diff
  function withdrawExpiredRewards(bytes32 id, address to) external onlyRole(REWARDS_MANAGER_ROLE) {
-   if (!_rewards[id].finalized || _rewards[id].amount == 0 || _rewards[id].expiry == 0 || _rewards[id].expiry > block.timestamp) {
+   if (!_rewards[id].finalized || _rewards[id].amount == 0 || _rewards[id].expiry == 0 || _rewards[id].expiry >= block.timestamp) {
      revert UnknownRewards();
    }

    uint256 amount = _rewards[id].amount;
    _rewards[id].amount = 0;
    yusd.safeTransfer(to, amount);

    emit WithdrawExpiredRewards(id, to, amount);
  }
```