Silly Mango Monkey

Medium

# Mint limit validation is not performed with actual mint amount

### Summary

When a user mints YUSD, the protocol ensures that the minting limit for the current period is not exceeded.
In `AegisMinting::mint` this is done by calling `AegisMinting::_checkMintRedeemLimit`.

The incorrect value is passed to this function and as a result some users' mint requests might revert when they shouldn't and the accounting for the current period's total minted amount will be incorrect.

### Root Cause

The issue is that `AegisMinting::_checkMintRedeemLimit` is called with the YUSD amount from the order ([AegisMinting.sol:250](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L250)), and not the actual amount that will be minted after price adjustment / slippage is accounted for ([AegisMinting.sol:254](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L254)).

```solidity
/**
   * @dev Mints YUSD from assets
   * @param order Struct containing order details
   * @param signature Signature of trusted signer
   */
  function mint(
    OrderLib.Order calldata order,
    bytes calldata signature
  ) external nonReentrant onlyWhitelisted(order.userWallet) onlySupportedAsset(order.collateralAsset) {
    if (mintPaused) {
      revert MintPaused();
    }
    if (order.orderType != OrderLib.OrderType.MINT) {
      revert InvalidOrder();
    }

    //@audit the value of yusdAmount calculated a few lines below should be used here instead of order.yusdAmount
    @>_checkMintRedeemLimit(mintLimit, order.yusdAmount);
    order.verify(getDomainSeparator(), aegisConfig.trustedSigner(), signature);
    _deduplicateOrder(order.userWallet, order.nonce);

    uint256 yusdAmount = _calculateMinYUSDAmount(order.collateralAsset, order.collateralAmount, order.yusdAmount);
    if (yusdAmount < order.slippageAdjustedAmount) {
      revert PriceSlippage();
    }

    // Take a fee, if it's applicable
    (uint256 mintAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(yusdAmount, mintFeeBP);
    if (fee > 0) {
      yusd.mint(insuranceFundAddress, fee);
    }

    IERC20(order.collateralAsset).safeTransferFrom(order.userWallet, address(this), order.collateralAmount);
    yusd.mint(order.userWallet, mintAmount);
    _custodyTransferrableAssetFunds[order.collateralAsset] += order.collateralAmount;

    emit Mint(_msgSender(), order.collateralAsset, order.collateralAmount, mintAmount, fee);
  }
```

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. User calls `AegisMinting::mint`

### Impact

If a user's actual mint amount is right on the threshold of the current period's mint limit, the mint request should be valid, but the `order.yusdAmount` might be just over the mint limit, which will cause the user's valid mint request to revert.

Even if the user's mint request does not revert due to crossing the mint limit threshold, the wrong amount will be appended to `limits.currentPeriodTotalAmount` as `order.yusdAmount` is not the actual amount that will be minted to the user.

### PoC

_No response_

### Mitigation

Call `AegistMinting::_checkMintRedeemLimit` after the actual mint amount has been calculated.
```diff
/**
   * @dev Mints YUSD from assets
   * @param order Struct containing order details
   * @param signature Signature of trusted signer
   */
  function mint(
    OrderLib.Order calldata order,
    bytes calldata signature
  ) external nonReentrant onlyWhitelisted(order.userWallet) onlySupportedAsset(order.collateralAsset) {
    if (mintPaused) {
      revert MintPaused();
    }
    if (order.orderType != OrderLib.OrderType.MINT) {
      revert InvalidOrder();
    }

-   _checkMintRedeemLimit(mintLimit, order.yusdAmount);
    order.verify(getDomainSeparator(), aegisConfig.trustedSigner(), signature);
    _deduplicateOrder(order.userWallet, order.nonce);

    uint256 yusdAmount = _calculateMinYUSDAmount(order.collateralAsset, order.collateralAmount, order.yusdAmount);
    if (yusdAmount < order.slippageAdjustedAmount) {
      revert PriceSlippage();
    }

+   _checkMintRedeemLimit(mintLimit, yusdAmount);

    // Take a fee, if it's applicable
    (uint256 mintAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(yusdAmount, mintFeeBP);
    if (fee > 0) {
      yusd.mint(insuranceFundAddress, fee);
    }

    IERC20(order.collateralAsset).safeTransferFrom(order.userWallet, address(this), order.collateralAmount);
    yusd.mint(order.userWallet, mintAmount);
    _custodyTransferrableAssetFunds[order.collateralAsset] += order.collateralAmount;

    emit Mint(_msgSender(), order.collateralAsset, order.collateralAmount, mintAmount, fee);
  }
```