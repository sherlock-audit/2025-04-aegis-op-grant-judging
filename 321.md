Clean Aquamarine Haddock

Medium

# Mint order can execute on wrong price, ineffective slippage check.

### Summary

There is slippage check which make sure the minimum about to be minted for the particular order, but due to wrong implementation. Slippage check is ineffective and could lead to order executing at wrong amount.

### Root Cause

In `AegisMiniting::mint` is the function which will use for minting the YUSD. [check here](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L239)
```solidity 

 function mint(
    OrderLib.Order calldata order,
    bytes calldata signature
  ) external nonReentrant onlyWhitelisted(order.userWallet) onlySupportedAsset(order.collateralAsset) {
    if (mintPaused) {
      revert MintPaused();
    }
    if (order.orderType != OrderLib.OrderType.MINT) {
      revert InvalidOrder();
    }

    _checkMintRedeemLimit(mintLimit, order.yusdAmount);
    order.verify(getDomainSeparator(), aegisConfig.trustedSigner(), signature);
    _deduplicateOrder(order.userWallet, order.nonce);

 @>   uint256 yusdAmount = _calculateMinYUSDAmount(order.collateralAsset, order.collateralAmount, order.yusdAmount);
  @>  if (yusdAmount < order.slippageAdjustedAmount) {
      revert PriceSlippage();
    }

    // Take a fee, if it's applicable
  @>  (uint256 mintAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(yusdAmount, mintFeeBP);
    if (fee > 0) {
      yusd.mint(insuranceFundAddress, fee);
    }

    IERC20(order.collateralAsset).safeTransferFrom(order.userWallet, address(this), order.collateralAmount);
  @>  yusd.mint(order.userWallet, mintAmount);
    _custodyTransferrableAssetFunds[order.collateralAsset] += order.collateralAmount;

    emit Mint(_msgSender(), order.collateralAsset, order.collateralAmount, mintAmount, fee);
  }
```

we are fetching `yusdAmount` according to the asset value of user, then we comparing this value with `order.slippageAdjustedAmount` which specified by the user the minimum value he need agains his `order.collateralAmount`. But `yusdAmount` is not the amount which we going to mint to the user, the actual amount is `mintAmount` which calculated after fees. 

So this check can pass here `yusdAmount < order.slippageAdjustedAmount` and user will still receive the less amount which  user is expecting



### Internal Pre-conditions

`mintFeeBP` should be set.

### External Pre-conditions

...

### Attack Path

works on every mint order.

### Impact

The user expect from function the minimum amount he would receive is `order.slippageAdjustedAmount` but user can receive the amount lower then this and function wouldn't revert. Because of slippage check before fee collection

### PoC
I have used foundry to write test, use this step for [integration](https://hardhat.org/hardhat-runner/docs/advanced/hardhat-and-foundry)

This POC state that how order can be executed then minimum amount expected


```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";
import {YUSD} from "contracts/YUSD.sol";
import {AegisMinting} from "contracts/AegisMinting.sol";
import {AegisConfig} from "contracts/AegisConfig.sol";
import {AegisOracle} from "contracts/AegisOracle.sol";
import {AegisRewards} from "contracts/AegisRewards.sol";

import {FeedRegistry} from "contracts/test/FeedRegistry.sol";
import {TestToken} from "contracts/test/TestToken.sol";

import {IAegisRewards} from "contracts/interfaces/IAegisRewards.sol";
import {IAegisMintingErrors} from "contracts/interfaces/IAegisMinting.sol";
import {IAegisConfig} from "contracts/interfaces/IAegisConfig.sol";
import {IAegisOracle} from "contracts/interfaces/IAegisOracle.sol";
import {IYUSD} from "contracts/interfaces/IYUSD.sol";
import {FeedRegistryInterface} from "@chainlink/contracts/src/v0.8/interfaces/FeedRegistryInterface.sol";
import {OrderLib} from  "contracts/lib/OrderLib.sol";
import {ClaimRewardsLib} from  "contracts/lib/ClaimRewardsLib.sol";


import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import "@openzeppelin/contracts/utils/cryptography/MessageHashUtils.sol";
import { Denominations } from "@chainlink/contracts/src/v0.8/Denominations.sol";

contract POC is Test {

    using ECDSA for bytes32;
    bytes32 internal constant ORDER_TYPE_HASH = keccak256(
        "Order(uint8 orderType,address userWallet,address collateralAsset,uint256 collateralAmount,uint256 yusdAmount,uint256 slippageAdjustedAmount,uint256 expiry,uint256 nonce,bytes additionalData)"
    );
    

    AegisMinting aeMint;
    IAegisConfig aegisConfig;
    AegisOracle aeOracle;
    AegisRewards aeRewards;

    YUSD yusd;
    TestToken mockERC20;
    FeedRegistry feedReg;

    address admin = makeAddr("admin");
    address oracle = makeAddr("oracle");
    address[] _assets;
    uint32[] heartbeat;
    address[] custodian;
    address[] ops;
    address _initialOwner = makeAddr("_initialOwner");
    address user = makeAddr("user");
    address _custodian = makeAddr("_custodian");
    address wallet = makeAddr("wallet");
    address _insuranceFundAddress = makeAddr("_insuranceFundAddress");

    bytes32 private constant SETTINGS_MANAGER_ROLE = keccak256("SETTINGS_MANAGER_ROLE");
    bytes32 private constant FUNDS_MANAGER_ROLE = keccak256("FUNDS_MANAGER_ROLE");
    bytes32 private constant COLLATERAL_MANAGER_ROLE = keccak256("COLLATERAL_MANAGER_ROLE");
    bytes32 private constant REWARDS_MANAGER_ROLE = keccak256("REWARDS_MANAGER_ROLE");

    uint256[] amounts;
    bytes32[]  ids;





    uint256 demoPrivateKey = 0x8db9a2e420ea6bf45ebd46411be83ae7f66cab825e04769f5c3a60b264d36c04;
    address trustedSigner = vm.addr(demoPrivateKey);

      using ClaimRewardsLib for ClaimRewardsLib.ClaimRequest;


    function setUp() public {
        // Correctly push into dynamic arrays

        mockERC20 = new TestToken("MOCK USDC", "USDC", 6);
        _assets.push(address(1));
        _assets.push(address(mockERC20));
        custodian.push(_custodian);

        ops.push(admin);

        heartbeat.push(5);
        heartbeat.push(0);

        // Optionally, if you want custodians as well, push addresses
        // custodian.push(address(4)); // Example if needed
        //   constructor(address _trustedSigner, address[] memory _ops, address _initialOwner) Ownable(_initialOwner) {
        
        yusd = new YUSD(admin);
        aeOracle = new AegisOracle(ops, _initialOwner);
        feedReg = new FeedRegistry();
        AegisConfig _aegisConfig = new AegisConfig(trustedSigner, ops, _initialOwner);


        IYUSD _yusd = IYUSD(address(yusd));
        aegisConfig = IAegisConfig(address(_aegisConfig));
        aeRewards = new AegisRewards(_yusd, aegisConfig, admin); 
        IAegisRewards _aegisRewards = IAegisRewards(address(2));
        IAegisOracle _aegisOracle = IAegisOracle(address(aeOracle));
        FeedRegistryInterface _fdRegistry = FeedRegistryInterface(address(feedReg));

        aeMint = new AegisMinting(
            _yusd,
            aegisConfig,
            _aegisRewards,
            _aegisOracle,
            _fdRegistry,
            _insuranceFundAddress, 
            _assets,
            heartbeat,
            custodian,
            admin
        );

        // grant roles
        vm.startPrank(admin);
        aeMint.grantRole(SETTINGS_MANAGER_ROLE, admin);
        aeMint.grantRole(FUNDS_MANAGER_ROLE, admin);
        aeMint.grantRole(COLLATERAL_MANAGER_ROLE, admin);
        aeMint.setMintFeeBP(2000);

        _aegisConfig.whitelistAddress(user, true);
        aeRewards.grantRole(REWARDS_MANAGER_ROLE, admin);
        aeRewards.setAegisMintingAddress(address(aeMint));
        vm.stopPrank();

         vm.startPrank(_aegisConfig.owner());
            _aegisConfig.setTrustedSigner(trustedSigner);
         vm.stopPrank();

        vm.prank(yusd.owner());
        yusd.setMinter(address(aeMint));

    }
    
    function testIneffectiveSlippageCheck() public {
        uint MinimumAmountExpected = 95 ether;
         OrderLib.Order memory order = buildOrder(
            0, // Redeem
            user,
            address(mockERC20),
            100e6,
            100 ether,
            MinimumAmountExpected,  // Minimum amount expected
            block.timestamp + 1 hours,
            1,
            abi.encode("Redeem Request 1")
        );

        
        bytes memory signature = signOrder(order, aeMint.getDomainSeparator(), demoPrivateKey);

        // Mock prices
        feedReg.setPrice(address(mockERC20), Denominations.USD, 1e8);
        vm.mockCall(
            address(aeOracle), 
            abi.encodeWithSelector(AegisOracle.yusdUSDPrice.selector),
            abi.encode(int256(1e8))
        );

        deal(address(mockERC20), user, 100e6);
        vm.startPrank(user);
        mockERC20.approve(address(aeMint), mockERC20.balanceOf(user));
        aeMint.mint(order, signature);
        vm.stopPrank();

        uint actualBalRec = yusd.balanceOf(user);
        console.log("MinimumAmountExpected", MinimumAmountExpected);
        console.log("actualBalRec",actualBalRec);
        assert(actualBalRec  < MinimumAmountExpected); // actualBalRec is less then expected and function still not revert

    }

      function buildOrder(
        uint8 orderType,
        address _user,
        address collateralAsset,
        uint256 collateralAmount,
        uint256 yusdAmount,
        uint256 slippageAdjustedAmount,
        uint256 expiry,
        uint256 nonce,
        bytes memory additionalData
    ) public pure returns (OrderLib.Order memory) {
        return OrderLib.Order({
            orderType: OrderLib.OrderType(orderType),
            userWallet: _user,
            collateralAsset: collateralAsset,
            collateralAmount: collateralAmount,
            yusdAmount: yusdAmount,
            slippageAdjustedAmount: slippageAdjustedAmount,
            expiry: expiry,
            nonce: nonce,
            additionalData: additionalData
        });
    }

    function signOrder(
        OrderLib.Order memory order,
        bytes32 domainSeparator,
        uint256 privateKey
    ) public pure returns (bytes memory) {
        bytes memory encoded = abi.encode(
            ORDER_TYPE_HASH,
            order.orderType,
            order.userWallet,
            order.collateralAsset,
            order.collateralAmount,
            order.yusdAmount,
            order.slippageAdjustedAmount,
            order.expiry,
            order.nonce,
            keccak256(order.additionalData)
        );

        bytes32 structHash = keccak256(encoded);
        bytes32 digest = MessageHashUtils.toTypedDataHash(domainSeparator, structHash);
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(privateKey, digest);
        return abi.encodePacked(r, s, v);
    }

}
```
### Mitigation

check slippage post fee calculation