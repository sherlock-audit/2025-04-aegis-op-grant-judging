Passive Umber Capybara

Medium

# Incorrect accounting of minted amount for the period

### Summary

In `AegisMinting`, for each period, there is a cap on how many YUSD can be minted, this is updated for each mint. However, the accounting is incorrect, which will cause a discrepancy between actual minted amount, and recorded amount.

### Root Cause

Here in the [`mint`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L239) function:
```solidity
  function mint(
    OrderLib.Order calldata order,
    bytes calldata signature
  ) external nonReentrant onlyWhitelisted(order.userWallet) onlySupportedAsset(order.collateralAsset) {
    if (mintPaused) {
      revert MintPaused();
    }
    if (order.orderType != OrderLib.OrderType.MINT) {
      revert InvalidOrder();
    }

    _checkMintRedeemLimit(mintLimit, order.yusdAmount);
    order.verify(getDomainSeparator(), aegisConfig.trustedSigner(), signature);
    _deduplicateOrder(order.userWallet, order.nonce);

    uint256 yusdAmount = _calculateMinYUSDAmount(order.collateralAsset, order.collateralAmount, order.yusdAmount);
    if (yusdAmount < order.slippageAdjustedAmount) {
      revert PriceSlippage();
    }
```

We see `_checkMintRedeemLimit` is called to ensure user does not mint more than limited:
```solidity
  function _checkMintRedeemLimit(MintRedeemLimit storage limits, uint256 yusdAmount) internal {
    if (limits.periodDuration == 0 || limits.maxPeriodAmount == 0) {
      return;
    }
    uint256 currentPeriodEndTime = limits.currentPeriodStartTime + limits.periodDuration;
    if (
      (currentPeriodEndTime >= block.timestamp && limits.currentPeriodTotalAmount + yusdAmount > limits.maxPeriodAmount) ||
      (currentPeriodEndTime < block.timestamp && yusdAmount > limits.maxPeriodAmount)
    ) {
      revert LimitReached();
    }
    // Start new mint period
    if (currentPeriodEndTime <= block.timestamp) {
      limits.currentPeriodStartTime = uint32(block.timestamp);
      limits.currentPeriodTotalAmount = 0;
    }

    limits.currentPeriodTotalAmount += yusdAmount;
  }
```

It adds the `yusdAmount`, which is directly from the input, `order.yusdAmount`. Later, in the `mint` function, we see minimum YUSD amount to be minted is calculated from oracle price, and this value can be lower than user's desired value, hence the slippage check. The issue here is, `limits.currentPeriodTotalAmount` is updated with user's desired value, not how much user actually will receive, creating a discrepancy here.

### Internal Pre-conditions

n/a

### External Pre-conditions

n/a

### Attack Path

n/a

### Impact

This incorrect accounting will cause less YUSD to be minted than actually recorded, for example, user may only be able to mint up to 1000 YUSD, but in reality, due to slippage and other price fluctuations, user only receives 950 YUSD.

### PoC

_No response_

### Mitigation

Update `mintLimit` with user received amount, instead of using `order.yusdAmount`.