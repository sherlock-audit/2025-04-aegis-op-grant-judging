Dandy Laurel Lizard

Medium

# Slippage check on mints/redeems is incorrectly applied before fees are deducted

### Summary

Slippage check on mints/redeems is incorrectly applied before fees are deducted.

### Root Cause

When users mint, a [slippage check](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/4aceb235db96b2299bb95ebf16e83a24f987bf3e/aegis-contracts/contracts/AegisMinting.sol#L255-L267) is made on the amount of yUSD the user will receive from the conversion based on oracle. The issue is that the slippage check is made **before** the fee is deducted:

```solidity
  function mint(
    OrderLib.Order calldata order,
    bytes calldata signature
  ) external nonReentrant onlyWhitelisted(order.userWallet) onlySupportedAsset(order.collateralAsset) {
    ...

    uint256 yusdAmount = _calculateMinYUSDAmount(order.collateralAsset, order.collateralAmount, order.yusdAmount);
@>  if (yusdAmount < order.slippageAdjustedAmount) { // @audit incorrect slippage check pre-fee application
      revert PriceSlippage();
    }

    // Take a fee, if it's applicable
    (uint256 mintAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(yusdAmount, mintFeeBP);
    if (fee > 0) {
      yusd.mint(insuranceFundAddress, fee);
    }

    IERC20(order.collateralAsset).safeTransferFrom(order.userWallet, address(this), order.collateralAmount);
    yusd.mint(order.userWallet, mintAmount);
    ...
  }
```

Example:

1. User wants to mint 10,100 tokens, slippage set to min 10,000 tokens
2. Conversion returns that the user will receive 10,000 tokens, thus the slippage check passes
3. 5% fee is deducted and user actually receives 9500 tokens
4. Slippage is incorrectly checked prior to taking fees

It's the same case during fulfillment of redeem requests.

Note: Both fees can be up to 50% so 5% is a valid example:

```solidity
  /// @dev Sets percent in basis points of YUSD that will be taken as a fee on mint
  function setMintFeeBP(uint16 value) external onlyRole(SETTINGS_MANAGER_ROLE) {
    // No more than 50%
    if (value > MAX_BPS / 2) {
      revert InvalidPercentBP(value);
    }
    mintFeeBP = value;
    emit SetMintFeeBP(value);
  }

  /// @dev Sets percent in basis points of YUSD that will be taken as a fee on redeem
  function setRedeemFeeBP(uint16 value) external onlyRole(SETTINGS_MANAGER_ROLE) {
    // No more than 50%
    if (value > MAX_BPS / 2) {
      revert InvalidPercentBP(value);
    }
    redeemFeeBP = value;
    emit SetRedeemFeeBP(value);
  }
```



### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

Not an attack

### Impact

Incorrectly applied slippage can return mints/redeems the user did not agree with.

### PoC

_No response_

### Mitigation

Make the slippage check only after deducting the fees