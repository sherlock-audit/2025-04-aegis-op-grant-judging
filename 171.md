Soaring Lemon Finch

Medium

# Inconsistent frozen asset calculation result in a doubling of the collateral amount being frozen

### Summary
Fund manager of Aegis.im can freeze collateral in `AegisMinting` contract.
The frozen collateral can be transferred to neither custodians nor users. The total frozen amount can not be above than the collateral balance in `AegisMinting`:
```solidity
  function freezeFunds(address asset, uint256 amount) external onlyRole(FUNDS_MANAGER_ROLE) onlySupportedAsset(asset) {
    if (assetFrozenFunds[asset] + amount > IERC20(asset).balanceOf(address(this))) {
      revert InvalidAmount();
    }
```
If the frozen amount equals the collateral balance in AegisMinting, no collateral can be transferred to custodians or users.

The protocol use [`_custodyAvailableAssetBalance()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L706-L714) to calculate how much collateral asset can be transferred to custody accounts, and use [`_untrackedAvailableAssetBalance()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L716-L723) to calculate how much collateral asset can be used for YUSD redeeming or rewarding.
```solidity
  function _custodyAvailableAssetBalance(address _asset) internal view returns (uint256) {
    uint256 custodyTransferrableFunds = _custodyTransferrableAssetFunds[_asset];
    uint256 balance = IERC20(_asset).balanceOf(address(this));
    if (balance < custodyTransferrableFunds || custodyTransferrableFunds < assetFrozenFunds[_asset]) {
      return 0;
    }

    return custodyTransferrableFunds - assetFrozenFunds[_asset];
  }

  function _untrackedAvailableAssetBalance(address _asset) internal view returns (uint256) {
    uint256 balance = IERC20(_asset).balanceOf(address(this));
    if (balance < _custodyTransferrableAssetFunds[_asset] + assetFrozenFunds[_asset]) {
      return 0;
    }

    return balance - _custodyTransferrableAssetFunds[_asset] - assetFrozenFunds[_asset];
  }
```
However, the Inconsistent calculation in above two functions could result in a doubling of the collateral amount being frozen. Below is an example:
Given $$balance_{USDC} = 100e6, custodyFund_{USDC} = 50e6, frozenFund_{USDC} = 50e6$$:
```math
\begin{align*}
custodyAvailable_{USDC} &= custodyFund_{USDC} - frozenFund_{USDC} \\
&= 50e6 - 50e6 \\
&= 0\\
untrackedAvailable_{USDC} &= balance_{USDC} - custodyFund_{USDC} - frozenFund_{USDC} \\
&= 100e6 - 50e6 - 50e6 \\
&= 0
\end{align*}
```
As can be seen, no USDC can be transferred to custodians or users even only half of 100e6 USDC in the contract is frozen.

### Root Cause

 Inconsistent frozen asset calculation result in a doubling of the collateral amount being frozen

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact
 Inconsistent frozen asset calculation result in a doubling of the collateral amount being frozen

### PoC

### Mitigation
Provide a clear definition of the frozen amount and use the correct calculation method. E.g. if  the custody collateral has priority to be frozen, and the frozen amount can be the whole balance, the calculation of untracked amount can be corrected as below:
```diff
  function _untrackedAvailableAssetBalance(address _asset) internal view returns (uint256) {
    uint256 balance = IERC20(_asset).balanceOf(address(this));
-   if (balance < _custodyTransferrableAssetFunds[_asset] + assetFrozenFunds[_asset]) {
-     return 0;
-    }

-   return balance - _custodyTransferrableAssetFunds[_asset] - assetFrozenFunds[_asset];
+   uint256 custodyOrFrozenFunds = _custodyTransferrableAssetFunds[_asset] > assetFrozenFunds[_asset] ? _custodyTransferrableAssetFunds[_asset] : assetFrozenFunds[_asset];
+   return balance > custodyOrFrozenFunds ? balance - custodyOrFrozenFunds : 0;
  }
```