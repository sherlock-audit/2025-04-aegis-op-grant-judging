Daring Pear Moth

High

# User rewards are permanently locked due to incorrect state tracking

### Summary

The [claimRewards()](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisRewards.sol#L91) function allows users to specify partial claim amounts `(claimRequest.amounts[i])`, but the `_addressClaimedRewards` mapping incorrectly marks the reward as fully claimed after the first partial claim. This prevents users from claiming the remaining balance, effectively locking unclaimed rewards permanently.



### Root Cause

In `AegisReward.sol:91-113`, It allows partial claiming through: `_rewards[claimRequest.ids[i]].amount -= claimRequest.amounts[i]`. This is a user-provided input struct allowing partial claiming.

However, `_addressClaimedRewards[user][ids[i]]` is set to true on the first claim, blocking future claims for the same `rewardId`. This creates a situation where if a user claims only a portion of their reward, the remaining balance becomes permanently inaccessible.

```solidity
/// @dev Transfers rewards at ids to a caller
  function claimRewards(ClaimRewardsLib.ClaimRequest calldata claimRequest, bytes calldata signature) external nonReentrant {
    claimRequest.verify(getDomainSeparator(), aegisConfig.trustedSigner(), signature);

    uint256 count = 0;
    uint256 totalAmount = 0;
    bytes32[] memory claimedIds = new bytes32[](claimRequest.ids.length);
    uint256 len = claimRequest.ids.length;
    for (uint256 i = 0; i < len; i++) {
      if (
        !_rewards[claimRequest.ids[i]].finalized ||
        _rewards[claimRequest.ids[i]].amount == 0 ||
        (_rewards[claimRequest.ids[i]].expiry > 0 && _rewards[claimRequest.ids[i]].expiry < block.timestamp) ||
        _addressClaimedRewards[_msgSender()][claimRequest.ids[i]]
      ) {
        continue;
      }

      _addressClaimedRewards[_msgSender()][claimRequest.ids[i]] = true;
      _rewards[claimRequest.ids[i]].amount -= claimRequest.amounts[i];
      totalAmount += claimRequest.amounts[i];
      claimedIds[count] = claimRequest.ids[i];
      count++;
    }

    if (totalAmount == 0) {
      revert ZeroRewards();
    }

    yusd.safeTransfer(_msgSender(), totalAmount);

    /// @solidity memory-safe-assembly
    assembly {
      mstore(claimedIds, count)
    }

    emit ClaimRewards(_msgSender(), claimedIds, totalAmount);
  }
```

https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisRewards.sol#L90C3-L125C1

### Internal Pre-conditions

1. Rewards are stored in `_rewards[ids[i]].amount.`

2. `_addressClaimedRewards[user][ids[i]]` tracks whether a user has claimed a reward (boolean).



### External Pre-conditions

1. User submits a ClaimRequest with `amounts[i] < _rewards[ids[i]].amount.`

2. User attempts to claim the remaining balance later.

### Attack Path

1. User claims a partial amount (e.g., 50 YUSD out of 100 YUSD).

2. The function deducts 50 YUSD from `_rewards[ids[i]].amount` (now 50 YUSD remains). Then sets `_addressClaimedRewards[user][ids[i]] = true.`

3. User tries to claim the remaining 50 YUSD: The check if (`_addressClaimedRewards[user][ids[i]]`) returns true and skips the reward. The remaining 50 YUSD is permanently locked.



### Impact

1. Users who perform partial claims permanently lose access to their remaining rewards.
2. This can result in substantial financial losses for users, especially for larger reward amounts.

### PoC

_No response_

### Mitigation

**Option 1: Full Claim Approach**
```solidity
function claimRewards(ClaimRewardsLib.ClaimRequest calldata claimRequest, bytes calldata signature) external nonReentrant {
  claimRequest.verify(getDomainSeparator(), aegisConfig.trustedSigner(), signature);

  uint256 count = 0;
  uint256 totalAmount = 0;
  bytes32[] memory claimedIds = new bytes32[](claimRequest.ids.length);
  uint256 len = claimRequest.ids.length;
  for (uint256 i = 0; i < len; i++) {
    if (
      !_rewards[claimRequest.ids[i]].finalized ||
      _rewards[claimRequest.ids[i]].amount == 0 ||
      (_rewards[claimRequest.ids[i]].expiry > 0 && _rewards[claimRequest.ids[i]].expiry < block.timestamp) ||
      _addressClaimedRewards[_msgSender()][claimRequest.ids[i]]
    ) {
      continue;
    }

    _addressClaimedRewards[_msgSender()][claimRequest.ids[i]] = true;
    totalAmount += _rewards[claimRequest.ids[i]].amount;  // Claim full amount
    _rewards[claimRequest.ids[i]].amount = 0;            // Set to zero
    claimedIds[count] = claimRequest.ids[i];
    count++;
  }
  
  // Rest of the function remains the same
}
```
**Option 2: Track Claimed Amounts**
```solidity
// Replace boolean mapping with amount tracking
mapping(address => mapping(bytes32 => uint256)) private _addressClaimedAmounts;

function claimRewards(ClaimRewardsLib.ClaimRequest calldata claimRequest, bytes calldata signature) external nonReentrant {
  claimRequest.verify(getDomainSeparator(), aegisConfig.trustedSigner(), signature);

  uint256 count = 0;
  uint256 totalAmount = 0;
  bytes32[] memory claimedIds = new bytes32[](claimRequest.ids.length);
  uint256 len = claimRequest.ids.length;
  for (uint256 i = 0; i < len; i++) {
    uint256 alreadyClaimed = _addressClaimedAmounts[_msgSender()][claimRequest.ids[i]];
    uint256 remainingAmount = _rewards[claimRequest.ids[i]].amount;
    
    if (
      !_rewards[claimRequest.ids[i]].finalized ||
      remainingAmount == 0 ||
      (_rewards[claimRequest.ids[i]].expiry > 0 && _rewards[claimRequest.ids[i]].expiry < block.timestamp) ||
      claimRequest.amounts[i] > remainingAmount
    ) {
      continue;
    }

    _addressClaimedAmounts[_msgSender()][claimRequest.ids[i]] += claimRequest.amounts[i];
    _rewards[claimRequest.ids[i]].amount -= claimRequest.amounts[i];
    totalAmount += claimRequest.amounts[i];
    claimedIds[count] = claimRequest.ids[i];
    count++;
  }
  
  // Rest of the function remains the same
}
```
**Note: Option 2 would need to be combined with proper signature replay protection to be secure.**