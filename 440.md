Mean Laurel Barracuda

Medium

# Incorrect decimal usage in the `_getAssetYUSDPriceOracle()` function

### Summary
The `_getAssetYUSDPriceOracle()` function is intended to return the price of the collateral asset in relation to YUSD, along with its decimal representation. However, it currently returns the decimal from `AegisOracle` instead of the decimal from `Chainlink`. This discrepancy can lead to DoS of redeemption process when the decimals differ.

### Root Cause
In the `_getAssetYUSDPriceOracle()` function, at [L829](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/4aceb235db96b2299bb95ebf16e83a24f987bf3e/aegis-contracts/contracts/AegisMinting.sol#L829), the function returns the price of a collateral asset in terms of YUSD along with the decimal from `AegisOracle`. Instead, it should return the decimal from `Chainlink`.

```solidity
    function _getAssetYUSDPriceOracle(address asset) internal view returns (uint256, uint8) {
        if (address(aegisOracle) == address(0)) {
        return (0, 0);
        }

        int256 yusdUSDPrice = aegisOracle.yusdUSDPrice();
        if (yusdUSDPrice == 0) {
        return (0, 0);
        }
        uint8 yusdUSDPriceDecimals = aegisOracle.decimals(); // @audit-info decimal from AegisOracle
        (uint256 assetUSDPrice, ) = _getAssetUSDPriceChainlink(asset); // @audit-info decimal from Chainlink

@>      return ((assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice), yusdUSDPriceDecimals);
        // @audit-info (decimal from Chainlink, decimal from AegisOracle)    
  }
```
Here, the decimal of `(assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice)` matches the decimal of `assetUSDPrice`, which is obtained from the `Chainlink`. However, the variable `yusdUSDPriceDecimals` represents the decimal of the price fetched from the `AegisOracle`.

These values are utilized at [L779](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/4aceb235db96b2299bb95ebf16e83a24f987bf3e/aegis-contracts/contracts/AegisMinting.sol#L779) to calculate the collateral amount. 
```solidity
        // Calculate collateral amount for aegisOracle asset/YUSD price.
@>      (uint256 oraclePrice, uint8 oracleDecimals) = _getAssetYUSDPriceOracle(collateralAsset);
        if (oraclePrice > 0) {      // @audit-info oraclePrice : decimal from Chainlink
        uint256 oracleCollateralAmount = Math.mulDiv(
            yusdAmount,
@>          10 ** oracleDecimals,   // @audit-info oracleDecimals : decimal from AegisOracle      
@>          oraclePrice * 10 ** (18 - IERC20Metadata(collateralAsset).decimals()) 
        );

        // Get smallest amount
        collateralAmount = Math.min(collateralAmount, oracleCollateralAmount);
        }
```
As illustrated above, the decimal of `oracleCollateralAmount` is not calculated correctly. It multiplies the decimal from `AegisOracle` and divides by the decimal from `Chainlink`. If their decimals differ, this will result in an incorrect value.

### Internal pre-conditions
N/A

### External pre-conditions
The decimal of the price fetched from Chainlink differs from 8(decimal from AegisOracle).

### Impact
If the decimal from Chainlink exceeds `8`, it will lead to DoS for all redeemption requests. As a result, users of YUSD can't redeem their collateral. It breaks the core functionality of the protocol.


### Mitigation
It is recommended to modify the code as follow:
```diff
    function _getAssetYUSDPriceOracle(address asset) internal view returns (uint256, uint8) {
        if (address(aegisOracle) == address(0)) {
        return (0, 0);
        }

        int256 yusdUSDPrice = aegisOracle.yusdUSDPrice();
        if (yusdUSDPrice == 0) {
        return (0, 0);
        }
        uint8 yusdUSDPriceDecimals = aegisOracle.decimals();
-       (uint256 assetUSDPrice, ) = _getAssetUSDPriceChainlink(asset);
+       (uint256 assetUSDPrice, uint8 assetDecimal) = _getAssetUSDPriceChainlink(asset);

-       return ((assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice), yusdUSDPriceDecimals);
+       return ((assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice), assetDecimal);        
    }
```