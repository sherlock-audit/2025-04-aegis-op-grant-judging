Proper Taffy Mantaray

High

# User redeem should not use YUSD oracle price, which breaks peg mechanism.


### Summary

User redeem should not use YUSD oracle price, which breaks peg mechanism.

### Root Cause

Let's first see how YUSD maintains peg. According to [docs](https://docs.aegis.im/overview/peg-stabilization-mechanism):

> Minting and Redeeming YUSD: When YUSD trades above `1$`, users can mint new YUSD by providing collateral, increasing the supply, and bringing the price back down. Conversely, when YUSD trades below `1$`, users can redeem YUSD for collateral, decreasing the supply and pushing the price back up.

Minting is simple. When YUSD trades above `1$`, users can mint YUSD by providing same value of collateral (as if YUSD is priced at `1$`) and sell YUSD for profit. This will bring down YUSD market price. Example, user can mint 1800 YUSD with 1 ETH at 1800 ETH/USD, then sell YUSD for profit.

Redeeming is similar. When YUSD trades below `1$`, user can redeem YUSD and receive the same amount of collateral. For example, user can first buy 1800 YUSD for less than `1800$`, than redeem 1800 YUSD for 1 ETH at 1800 ETH/USD, and profit.

The issue here is when redeeming, the actual amount of collateral received also involves YUSD oracle price. This is particularly bad, because this will break the peg arbitrage for user redeem. In the above example, if YUSD is at `0.95$`, then when users redeem 1800 YUSD for ETH, they can only get `1800 / (1800 / 0.95) = 0.95 ETH`, which is not profitable at all.

Similar stablecoin contracts (e.g. [Liquity](https://www.liquity.org/blog/understanding-liquitys-redemption-mechanism)) always assume the stablecoin is `1$` during mint and redeems, which is the core feature of the pegging mechanism.

https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L315

```solidity
  function approveRedeemRequest(string calldata requestId, uint256 amount) external nonReentrant onlyRole(FUNDS_MANAGER_ROLE) whenRedeemUnpaused {
    ...
@>  uint256 collateralAmount = _calculateRedeemMinCollateralAmount(request.order.collateralAsset, amount, request.order.yusdAmount);
    ...
  }

  function _calculateRedeemMinCollateralAmount(
    address collateralAsset,
    uint256 collateralAmount,
    uint256 yusdAmount
  ) internal view returns (uint256) {
    // Calculate collateral amount for chainlink asset price.
    (uint256 chainlinkPrice, uint8 feedDecimals) = _getAssetUSDPriceChainlink(collateralAsset);
    if (chainlinkPrice > 0) {
      uint256 chainlinkCollateralAmount = Math.mulDiv(
        yusdAmount,
        10 ** feedDecimals,
        chainlinkPrice * 10 ** (18 - IERC20Metadata(collateralAsset).decimals())
      );

      // Get smallest amount
      collateralAmount = Math.min(collateralAmount, chainlinkCollateralAmount);
    }

    // Calculate collateral amount for aegisOracle asset/YUSD price.
@>  (uint256 oraclePrice, uint8 oracleDecimals) = _getAssetYUSDPriceOracle(collateralAsset);
    if (oraclePrice > 0) {
      uint256 oracleCollateralAmount = Math.mulDiv(
        yusdAmount,
        10 ** oracleDecimals,
        oraclePrice * 10 ** (18 - IERC20Metadata(collateralAsset).decimals())
      );

      // Get smallest amount
      collateralAmount = Math.min(collateralAmount, oracleCollateralAmount);
    }

    return collateralAmount;
  }

  function _getAssetYUSDPriceOracle(address asset) internal view returns (uint256, uint8) {
    if (address(aegisOracle) == address(0)) {
      return (0, 0);
    }

    int256 yusdUSDPrice = aegisOracle.yusdUSDPrice();
    if (yusdUSDPrice == 0) {
      return (0, 0);
    }
    uint8 yusdUSDPriceDecimals = aegisOracle.decimals();
    (uint256 assetUSDPrice, ) = _getAssetUSDPriceChainlink(asset);

    return ((assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice), yusdUSDPriceDecimals);
  }

```

### Internal pre-conditions

YUSD depegs below `1$`.

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

When YUSD depegs below `1$`, the redeem pegging mechanism would break.

### PoC

N/A

### Mitigation

Do not use YUSD oracle to calculate the amount of collateral asset. Always assume YUSD is `1$` during redeem.
