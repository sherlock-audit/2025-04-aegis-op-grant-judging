Soaring Lemon Finch

High

# The price and decimals returned from `_getAssetYUSDPriceOracle()` could be mismatched

### Summary
A whitelisted user can call [`AegisMinting#requestRedeem()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L277-L307) to initial a request to redeem YUSD for supported asset like USDC, USDT, DAI. 
Funds manager will approve the request by calling [`AegisMinting#approveRedeemRequest()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L315-L358) and the redeemed collateral will be transferred to the redeemer:
```solidity
  function approveRedeemRequest(string calldata requestId, uint256 amount) external nonReentrant onlyRole(FUNDS_MANAGER_ROLE) whenRedeemUnpaused {
    RedeemRequest storage request = _redeemRequests[keccak256(abi.encode(requestId))];
    if (request.timestamp == 0 || request.status != RedeemRequestStatus.PENDING) {
      revert InvalidRedeemRequest();
    }
    if (amount == 0 || amount > request.order.collateralAmount) {
      revert InvalidAmount();
    }

@>  uint256 collateralAmount = _calculateRedeemMinCollateralAmount(request.order.collateralAsset, amount, request.order.yusdAmount);
    /*
     * Reject if:
     * - asset is no longer supported
     * - smallest amount is less than order minAmount
     * - order expired
     */
    if (
      !_supportedAssets.contains(request.order.collateralAsset) ||
      collateralAmount < request.order.slippageAdjustedAmount ||
      request.order.expiry < block.timestamp
    ) {
      _rejectRedeemRequest(requestId, request);
      return;
    }

    uint256 availableAssetFunds = _untrackedAvailableAssetBalance(request.order.collateralAsset);
    if (availableAssetFunds < collateralAmount) {
      revert NotEnoughFunds();
    }

    // Take a fee, if it's applicable
    (uint256 burnAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(request.order.yusdAmount, redeemFeeBP);
    if (fee > 0) {
      yusd.safeTransfer(insuranceFundAddress, fee);
    }

    request.status = RedeemRequestStatus.APPROVED;
    totalRedeemLockedYUSD -= request.order.yusdAmount;

    IERC20(request.order.collateralAsset).safeTransfer(request.order.userWallet, collateralAmount);
    yusd.burn(burnAmount);

    emit ApproveRedeemRequest(requestId, _msgSender(), request.order.userWallet, request.order.collateralAsset, collateralAmount, burnAmount, fee);
  }
```
`_calculateRedeemMinCollateralAmount()` is used to calculate how much collateral can be redeemed with specified amount of YUSD:
```solidity
  function _calculateRedeemMinCollateralAmount(
    address collateralAsset,
    uint256 collateralAmount,
    uint256 yusdAmount
  ) internal view returns (uint256) {
    // Calculate collateral amount for chainlink asset price.
    (uint256 chainlinkPrice, uint8 feedDecimals) = _getAssetUSDPriceChainlink(collateralAsset);
    if (chainlinkPrice > 0) {
      uint256 chainlinkCollateralAmount = Math.mulDiv(
        yusdAmount,
        10 ** feedDecimals,
        chainlinkPrice * 10 ** (18 - IERC20Metadata(collateralAsset).decimals())
      );

      // Get smallest amount
      collateralAmount = Math.min(collateralAmount, chainlinkCollateralAmount);
    }

    // Calculate collateral amount for aegisOracle asset/YUSD price.
@>  (uint256 oraclePrice, uint8 oracleDecimals) = _getAssetYUSDPriceOracle(collateralAsset);
    if (oraclePrice > 0) {
      uint256 oracleCollateralAmount = Math.mulDiv(
        yusdAmount,
        10 ** oracleDecimals,
        oraclePrice * 10 ** (18 - IERC20Metadata(collateralAsset).decimals())
      );

      // Get smallest amount
      collateralAmount = Math.min(collateralAmount, oracleCollateralAmount);
    }

    return collateralAmount;
  }

  function _getAssetYUSDPriceOracle(address asset) internal view returns (uint256, uint8) {
    if (address(aegisOracle) == address(0)) {
      return (0, 0);
    }

    int256 yusdUSDPrice = aegisOracle.yusdUSDPrice();
    if (yusdUSDPrice == 0) {
      return (0, 0);
    }
    uint8 yusdUSDPriceDecimals = aegisOracle.decimals();
    (uint256 assetUSDPrice, ) = _getAssetUSDPriceChainlink(asset);

    //@audit-info incorrect yusdUSDPriceDecimals is returned as decimals
@>  return ((assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice), yusdUSDPriceDecimals);
  }
```
However, mismatched price value and decimals during YUSD asset price calculation can lead to `approveRedeemRequest()` failure or incorrect collateral redemption.

### Root Cause

Price value and decimals returned from  `_getAssetYUSDPriceOracle()` are mismatched

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact
Mismatched price value and decimals during YUSD asset price calculation can lead to `approveRedeemRequest()` failure or incorrect collateral redemption.
### PoC
Craft `MockFeedRegistry` as feed registry:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

contract MockFeedRegistry {
  uint256 updatedAt;

  mapping(address => mapping(address => int256)) private _prices;

  function decimals(address, address) external pure returns (uint8) {
    return 18;
  }

  function latestRoundData(address base, address quote) external view returns (uint80, int256, uint256, uint256, uint80) {
    return (1, _prices[base][quote], block.timestamp, updatedAt == 0 ? block.timestamp : updatedAt, 1);
  }

  function setPrice(address base, address quote, int256 price) external {
    _prices[base][quote] = price;
  }

  function setUpdatedAt(uint256 val) external {
    updatedAt = val;
  }
}
```
Copy below codes into [6_aegis_minting_redeem.spec.ts](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/test/6_aegis_minting_redeem.spec.ts) and run `yarn test`:
```solidity
  describe('#_getAssetYUSDPriceOracle', () => {
    it.only('the price value is mismatched decimals', async () => {
      const [owner, sender] = await ethers.getSigners()

      const { aegisMintingContract, aegisMintingAddress, assetAddress, yusdContract, assetContract, aegisConfig } = await loadFixture(deployFixture)

      const feedRegistry = await ethers.deployContract('MockFeedRegistry')
      await aegisMintingContract.grantRole(SETTINGS_MANAGER_ROLE, owner)
      await aegisMintingContract.setFeedRegistryAddress(feedRegistry)

      //@audit-info the decimals in feedRegistry is 18 
      await expect(feedRegistry.decimals(assetContract, USD_FEED_ADDRESS)).to.be.eventually.equal(18)
      // Set feed price to 1 asset/USD to pass check (1e18)
      await feedRegistry.setPrice(assetContract, USD_FEED_ADDRESS, '1000000000000000000')

      const aegisOracle = await ethers.deployContract('AegisOracle', [[owner], owner])
      await aegisMintingContract.setAegisOracleAddress(aegisOracle)
      await aegisOracle.updateYUSDPrice('99963000')
      console.log("Asset Price in YUSD:", await aegisMintingContract.assetAegisOracleYUSDPrice(assetContract))
      console.log("Asset/USD decimals:", await feedRegistry.decimals(assetContract, USD_FEED_ADDRESS))
      console.log("YUSD/USD decimals:", await aegisOracle.decimals())
    })
  })
```
The result of running test case is as below:
```solidity
  AegisMinting
    #_getAssetYUSDPriceOracle
Asset Price in YUSD: 1000370136950671748n
Asset/USD decimals: 18n
YUSD/USD decimals: 8n
      âœ” the price value is mismatched decimals (1356ms)


  1 passing (1s)
```
As can be seen, the price and decimals returned from _getAssetYUSDPriceOracle() are mismatched.
### Mitigation
```diff
  function _getAssetYUSDPriceOracle(address asset) internal view returns (uint256, uint8) {
    if (address(aegisOracle) == address(0)) {
      return (0, 0);
    }

    int256 yusdUSDPrice = aegisOracle.yusdUSDPrice();
    if (yusdUSDPrice == 0) {
      return (0, 0);
    }
    uint8 yusdUSDPriceDecimals = aegisOracle.decimals();
-   (uint256 assetUSDPrice, ) = _getAssetUSDPriceChainlink(asset);

-   return ((assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice), yusdUSDPriceDecimals);

+   (uint256 assetUSDPrice, uint assetUSDDecimals) = _getAssetUSDPriceChainlink(asset);

+   return ((assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice), assetUSDDecimals);
  }
```