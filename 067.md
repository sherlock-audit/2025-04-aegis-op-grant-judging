Rich Hazelnut Crow

High

# Malicious users can DoS the redeem limit by creating non-executable orders and withdrawing immediately

### Summary

Since the redeem limit is increased during the creation of redeem requests, as well as there are ways to create redeem requests which can't be accepted (and can be immediately cancelled), a malicious user is able to DoS the limit every period preventing any redeems to take place.

### Root Cause

The main root cause to this vulnerability is that the redeem limit is increased during the creation of redeem requests, regardless of whether they would be accepted or rejected.

https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L277-L285

And the actual limit check and increase:

```solidity

  function _checkMintRedeemLimit(MintRedeemLimit storage limits, uint256 yusdAmount) internal {
    if (limits.periodDuration == 0 || limits.maxPeriodAmount == 0) {
      return;
    }
    uint256 currentPeriodEndTime = limits.currentPeriodStartTime + limits.periodDuration;
    if (
      (currentPeriodEndTime >= block.timestamp && limits.currentPeriodTotalAmount + yusdAmount > limits.maxPeriodAmount) ||
      (currentPeriodEndTime < block.timestamp && yusdAmount > limits.maxPeriodAmount)
    ) {
      revert LimitReached();
    }
    // Start new mint period
    if (currentPeriodEndTime <= block.timestamp) {
      limits.currentPeriodStartTime = uint32(block.timestamp);
      limits.currentPeriodTotalAmount = 0;
    }


    limits.currentPeriodTotalAmount += yusdAmount;
  }

```

Since the limit is per period, this allows malicious users to easily DoS the current period's limit as well as all following ones by creating redeem requests which can't be accepted. 

There are multiple ways to create such requests, including but not limited to:
- Creating requests with very short expiries, including such that can immediately be withdrawn (in the same block);
- Creating redeem requests for a collateral for which the user wallet in-question is on a blacklist (as the system accepts both USDC and USDT, both of these have a blacklist);

If we take the first scenario into question, this would allow the user to create a redeem request with the full limit amount which expires in the current block. Due to how the expiry verification is set up, this will be accepted as a legitimate request:
```solidity

  function verify(
    Order calldata self,
    bytes32 domainSeparator,
    address expectedSigner,
    bytes calldata signature
  ) internal view returns (bytes32 orderHash) {
    orderHash = hashOrder(self, domainSeparator);
    address signer = ECDSA.recover(orderHash, signature);

    if (self.userWallet != msg.sender) revert InvalidSender();
    if (signer != expectedSigner) revert InvalidSignature();
    if (self.collateralAmount == 0) revert InvalidAmount();
    if (self.yusdAmount == 0) revert InvalidAmount();
    if (block.timestamp > self.expiry) revert SignatureExpired();
  }


```

As it can be seen the expiry validation will pass as it only checks if the block.timestamp is greater than the set expiry (but not equal to).
As mentioned above, since the redeem limit is increased during the request creation (regardless of whether the request will be accepted/rejected), this prompted the limit to be maxed out for the current period.

Following this, the attacker is able to immediately cancel the redeem request: 

```solidity

  function withdrawRedeemRequest(string calldata requestId) public nonReentrant whenRedeemUnpaused {
    RedeemRequest storage request = _redeemRequests[keccak256(abi.encode(requestId))];
    if (request.timestamp == 0 || request.status != RedeemRequestStatus.PENDING || request.order.expiry > block.timestamp) {
      revert InvalidRedeemRequest();
    }


    request.status = RedeemRequestStatus.WITHDRAWN;


    // Unlock YUSD
    totalRedeemLockedYUSD -= request.order.yusdAmount;
    yusd.safeTransfer(request.order.userWallet, request.order.yusdAmount);


    emit WithdrawRedeemRequest(requestId, request.order.userWallet, request.order.yusdAmount);
  }

```
This is because the validation here checks if the order expiry is greater than the current timestamp, i.e. if it's equal, the request is cancellable.
Although the exploitation path taken in this report focuses on creating immediately cancellable requests via setting the expiry as the current block.timestamp for the purpose of DoSing the redeem limit as mentioned above, there are other possible ways in which non-acceptable redeem requests could be created.

### Internal Pre-conditions

1. The redeem limit is increased during redeem request creation, regarding of whether the request is accepted, rejected or cancelled.
2. There are ways in which users could create non-acceptable redeem requests, or ones which can be immediately cancelled.

### External Pre-conditions

User creates a redeem request which will revert during acceptance, or creates immediately cancellable requests, which are submitted and cancelled in the same block for the purpose of DoSing the redeem limits.

### Attack Path

For the purpose of this example, we're assuming that a period lasts 24 hours or 86400 seconds, and the maximum redeem amount is 250_000 YUSD during this period.

1. Current period's `currentPeriodTotalAmount` is 0.
2. A malicious actor creates a redeem request for 250_000e18 YUSD at the beginning of the current period with an expiry value set as the current `block.timestamp`;
3. The current limit is increased `currentPeriodTotalAmount` to 250_000e18 which will make all subsequent redeem requests fail due to the reached limit during this period.
4. The malicious user immediately cancels the request receiving all 250_000e18 YUSD back, while the limit remains DoSd.

This attack can be performed with smaller amounts by posting multiple requests which will be immediately cancelled and DoSing the said limit;
If the limit is increased by the Governence, they can re-perform this attack as many times as needed in order to DoS it again.

### Impact

The redeem limit can be easily DoSd by creating requests which can be cancelled in the same block. This will prevent any other redeem requests being placed during that period.

### PoC

_No response_

### Mitigation

There are several ways in which this issue could be addressed:
- Tracking the pending requests in a separate "virtual" limit which is checked upon the creation of the request, but is decreased if the request is withdrawn or rejected;
- Increasing the "real" limit only when a request is accepted;

A secondary mediation would be not allowing immediately cancellable requests to be placed, but this would still allow for other ways to create a request which can't be accepted (i.e. reverts on acceptance). 