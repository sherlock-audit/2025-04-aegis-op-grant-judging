Tangy Dijon Fly

High

# `_calculateRedeemMinCollateralAmount()` Uses Incorrect `oracleDecimals`.

### Summary

The `_calculateRedeemMinCollateralAmount()` function is incorrect because it utilizes `yusdUSDPriceDecimals` instead of `feedDecimals` as the `oracleDecimals`.

### Root Cause

The [_calculateRedeemMinCollateralAmount()](https://github.com/sherlock-audit/2025-04-aegis-op-grant/tree/main/aegis-contracts/contracts/AegisMinting.sol#L770) function retrieves `oracleDecimals` from `_getAssetYUSDPriceOracle()`. However, the [_getAssetYUSDPriceOracle()](https://github.com/sherlock-audit/2025-04-aegis-op-grant/tree/main/aegis-contracts/contracts/AegisMinting.sol#L829) function returns `yusdUSDPriceDecimals`.

As a result, `yusdUSDPriceDecimals` is incorrectly used as `oracleDecimals`, leading to an inaccurate calculation of `oracleCollateralAmount`.

**WHY?** Let's consider the following scenario:

- `yusdAmount` = 100 e18 (representing `100 YUSD`)
- `yusdUSDPriceDecimals` = 8 (as indicated in [AegisOracle](https://github.com/sherlock-audit/2025-04-aegis-op-grant/tree/main/aegis-contracts/contracts/AegisOracle.sol#L39))
- `yusdUSDPrice` = 1 e8 (representing `1 YUSD = 1 USD`)
- `feedDecimals` = 18
- `assetUSDPrice` = 2 e18 (representing that `1 collateral = 2 YUSD`)
- `collateralAsset.decimals` = 18

In this scenario, the `_getAssetYUSDPriceOracle()` function returns `(2e18, 8)`, resulting in `oraclePrice = 2e18` and `oracleDecimals = 8`. Therefore, the calculation for `oracleCollateralAmount` becomes:
```solidity
    oracleCollateralAmount = 100 e18 * 10 ** 8 / (2e18 * 10 ** (18 - 18)) = 50e8
```
Since the collateral's decimal is 18, `50e8` is a very small amount. The correct collateral amount should be `50e18`, as `1 collateral = 2 YUSD`. This demonstrates why `feedDecimals` should be used as `oracleDecimals`.

```solidity
      function _calculateRedeemMinCollateralAmount(
        address collateralAsset,
        uint256 collateralAmount,
        uint256 yusdAmount
      ) internal view returns (uint256) {
        // Calculate collateral amount for chainlink asset price.
        (uint256 chainlinkPrice, uint8 feedDecimals) = _getAssetUSDPriceChainlink(collateralAsset);
        if (chainlinkPrice > 0) {
          uint256 chainlinkCollateralAmount = Math.mulDiv(
            yusdAmount,
            10 ** feedDecimals,
            chainlinkPrice * 10 ** (18 - IERC20Metadata(collateralAsset).decimals())
          );

          // Get smallest amount
          collateralAmount = Math.min(collateralAmount, chainlinkCollateralAmount);
        }

        // Calculate collateral amount for aegisOracle asset/YUSD price.
@>      (uint256 oraclePrice, uint8 oracleDecimals) = _getAssetYUSDPriceOracle(collateralAsset);
        if (oraclePrice > 0) {
          uint256 oracleCollateralAmount = Math.mulDiv(
            yusdAmount,
@>          10 ** oracleDecimals,
            oraclePrice * 10 ** (18 - IERC20Metadata(collateralAsset).decimals())
          );

          // Get smallest amount
          collateralAmount = Math.min(collateralAmount, oracleCollateralAmount);
        }

        return collateralAmount;
      }

-----------------------------------------------------------------------------------------------------

      function _getAssetYUSDPriceOracle(address asset) internal view returns (uint256, uint8) {
        if (address(aegisOracle) == address(0)) {
          return (0, 0);
        }

        int256 yusdUSDPrice = aegisOracle.yusdUSDPrice();
        if (yusdUSDPrice == 0) {
          return (0, 0);
        }
        uint8 yusdUSDPriceDecimals = aegisOracle.decimals();
        (uint256 assetUSDPrice, ) = _getAssetUSDPriceChainlink(asset);

@>      return ((assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice), yusdUSDPriceDecimals);
      }
```

### Internal pre-conditions

### External pre-conditions

### Attack Path

### Impact

This issue results in an incorrect redeem amount being calculated.

### PoC

### Mitigation

Use `feedDecimals` as `oracleDecimals` to ensure accurate calculations.

```diff
      function _calculateRedeemMinCollateralAmount(
        address collateralAsset,
        uint256 collateralAmount,
        uint256 yusdAmount
      ) internal view returns (uint256) {
        // Calculate collateral amount for chainlink asset price.
        (uint256 chainlinkPrice, uint8 feedDecimals) = _getAssetUSDPriceChainlink(collateralAsset);
        if (chainlinkPrice > 0) {
          uint256 chainlinkCollateralAmount = Math.mulDiv(
            yusdAmount,
            10 ** feedDecimals,
            chainlinkPrice * 10 ** (18 - IERC20Metadata(collateralAsset).decimals())
          );

          // Get smallest amount
          collateralAmount = Math.min(collateralAmount, chainlinkCollateralAmount);
        }

        // Calculate collateral amount for aegisOracle asset/YUSD price.
        (uint256 oraclePrice, uint8 oracleDecimals) = _getAssetYUSDPriceOracle(collateralAsset);
        if (oraclePrice > 0) {
          uint256 oracleCollateralAmount = Math.mulDiv(
            yusdAmount,
-           10 ** oracleDecimals,
+           10 ** feedDecimals,
            oraclePrice * 10 ** (18 - IERC20Metadata(collateralAsset).decimals())
          );

          // Get smallest amount
          collateralAmount = Math.min(collateralAmount, oracleCollateralAmount);
        }

        return collateralAmount;
      }
```