Beautiful Raspberry Crane

Medium

# Fee calculation logic in `approveRedeemRequest` is incorrect

### Summary

In the `approveRedeemRequest` function, there is an issue with the fee calculation logic. The fee should be paid by the user who is redeeming, but instead, the fee is left for the subsequent user. This results in incorrect asset-liability accounting in the protocol, leading to potential financial discrepancies.

### Root Cause

The root cause is in [here](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/4aceb235db96b2299bb95ebf16e83a24f987bf3e/aegis-contracts/contracts/AegisMinting.sol#L346) that the `collateralAmount` paid to the user remains unchanged, while the fee is separately paid to the `insuranceFundAddress`. This discrepancy causes financial issues to accumulate over time. By not properly accounting for the fee within the transaction, the protocol leaves the burden of this imbalance to future users. As a result, the protocol's asset-liability calculations become distorted, causing potential financial instability and inequities.

```solidity
@>  (uint256 burnAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(request.order.yusdAmount, redeemFeeBP);
    if (fee > 0) {
      yusd.safeTransfer(insuranceFundAddress, fee);
    }

    request.status = RedeemRequestStatus.APPROVED;
    totalRedeemLockedYUSD -= request.order.yusdAmount;

@>  IERC20(request.order.collateralAsset).safeTransfer(request.order.userWallet, collateralAmount);
```

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

Calling `approveRedeemRequest` can cause an error in the accounting of funds.

### Impact

The protocol will inaccurately track its liabilities, leading to financial instability or incorrect financial statements.

### PoC

Run with `forge test --mt testWeirdFeeRules -vvv`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

import "forge-std/Test.sol";
import "@openzeppelin/contracts/utils/cryptography/MessageHashUtils.sol";

import { YUSD } from "../contracts/YUSD.sol";
import { AegisRewards } from "../contracts/AegisRewards.sol";
import { AegisConfig } from "../contracts/AegisConfig.sol";
import { AegisMinting } from "../contracts/AegisMinting.sol";
import { AegisOracle } from "../contracts/AegisOracle.sol";

import { TestToken } from "../contracts/test/TestToken.sol";
import { FeedRegistry } from "../contracts/test/FeedRegistry.sol";

import { IAegisMintingEvents, IAegisMintingErrors } from "../contracts/interfaces/IAegisMinting.sol";
import { IAegisRewards } from "../contracts/interfaces/IAegisRewards.sol";
import { IAegisConfig } from "../contracts/interfaces/IAegisConfig.sol";
import { IAegisOracle } from "../contracts/interfaces/IAegisOracle.sol";
import { IYUSD } from "../contracts/interfaces/IYUSD.sol";
import { FeedRegistryInterface } from "@chainlink/contracts/src/v0.8/interfaces/FeedRegistryInterface.sol";

import {OrderLib} from "../contracts/lib/OrderLib.sol";
import {ClaimRewardsLib} from "../contracts/lib/ClaimRewardsLib.sol";

import { Denominations } from "@chainlink/contracts/src/v0.8/Denominations.sol";

contract BasicTest is Test {
    using OrderLib for OrderLib.Order;
    using ClaimRewardsLib for ClaimRewardsLib.ClaimRequest;
    bytes32 private constant FUNDS_MANAGER_ROLE = keccak256("FUNDS_MANAGER_ROLE");
    bytes32 private constant SETTINGS_MANAGER_ROLE = keccak256("SETTINGS_MANAGER_ROLE");
    bytes32 private constant COLLATERAL_MANAGER_ROLE = keccak256("COLLATERAL_MANAGER_ROLE");

    bytes32 private constant REWARDS_MANAGER_ROLE = keccak256("REWARDS_MANAGER_ROLE");


    bytes32 private constant ORDER_TYPE =
      keccak256(
        "Order(uint8 orderType,address userWallet,address collateralAsset,uint256 collateralAmount,uint256 yusdAmount,uint256 slippageAdjustedAmount,uint256 expiry,uint256 nonce,bytes additionalData)"
      );
    bytes32 private constant CLAIM_REQUEST_TYPE = keccak256("ClaimRequest(address claimer,bytes32[] ids,uint256[] amounts)");

    bytes32 private DomainSeparator_AegisMinting;
    bytes32 private DomainSeparator_AegisRewards;

    YUSD public yusd;
    TestToken public assetToken;
    AegisConfig public aegisConfig;
    AegisRewards public aegisRewards;
    AegisMinting public aegisMinting;
    AegisOracle public aegisOracle;
    FeedRegistry public feedRegistry;

    Vm.Wallet trustedSignerAccount = vm.createWallet("trustedSignerAccount");
    address custodian = makeAddr("custodian");
    address insuranceFund = makeAddr("insuranceFund");

    address owner = makeAddr("owner");

    function setUp() virtual public {

        assetToken = new TestToken("Test", "TST", 18);
        yusd = new YUSD(owner);

        address[] memory ops = new address[](1);
        ops[0] = owner;
        aegisConfig = new AegisConfig(trustedSignerAccount.addr, ops, owner);
        aegisRewards = new AegisRewards(IYUSD(address(yusd)), IAegisConfig(address(aegisConfig)), owner);
        aegisOracle = new AegisOracle(ops, owner);
        feedRegistry = new FeedRegistry();

        address[] memory assets = new address[](1);
        assets[0] = address(assetToken);
        uint32[] memory chainlinkAssetHeartbeats = new uint32[](1);
        chainlinkAssetHeartbeats[0] = 0;
        address[] memory custodians = new address[](1);
        custodians[0] = custodian;
        aegisMinting = new AegisMinting(
            IYUSD(address(yusd)),
            IAegisConfig(address(aegisConfig)),
            IAegisRewards(address(aegisRewards)),
            IAegisOracle(address(aegisOracle)),
            FeedRegistryInterface(address(feedRegistry)),
            insuranceFund,
            assets,
            chainlinkAssetHeartbeats,
            custodians,
            owner
        );

        vm.startPrank(owner);
        yusd.setMinter(address(aegisMinting));
        aegisRewards.setAegisMintingAddress(address(aegisMinting));

        aegisMinting.grantRole(FUNDS_MANAGER_ROLE, owner);
        aegisMinting.grantRole(SETTINGS_MANAGER_ROLE, owner);
        aegisMinting.grantRole(COLLATERAL_MANAGER_ROLE, owner);

        aegisRewards.grantRole(REWARDS_MANAGER_ROLE, owner);

        vm.stopPrank();

        DomainSeparator_AegisMinting = aegisMinting.getDomainSeparator();
        DomainSeparator_AegisRewards = aegisRewards.getDomainSeparator();
    }

    function signOrder(uint256 signerPk, OrderLib.Order memory order) public returns (bytes memory) {
      bytes memory encodeOrder = abi.encode(
        ORDER_TYPE,
        order.orderType,
        order.userWallet,
        order.collateralAsset,
        order.collateralAmount,
        order.yusdAmount,
        order.slippageAdjustedAmount,
        order.expiry,
        order.nonce,
        keccak256(order.additionalData)
      );
      bytes32 dataHash = MessageHashUtils.toTypedDataHash(DomainSeparator_AegisMinting, keccak256(encodeOrder));
      return toSign(signerPk, dataHash);
    }

    function signClaimRequest(uint256 signerPk, ClaimRewardsLib.ClaimRequest memory claimRequest) public returns (bytes memory) {
      bytes memory encodeClaimRequest = abi.encode(
        CLAIM_REQUEST_TYPE,
        claimRequest.claimer,
        keccak256(abi.encodePacked(claimRequest.ids)),
        keccak256(abi.encodePacked(claimRequest.amounts))
      );
      bytes32 dataHash = MessageHashUtils.toTypedDataHash(DomainSeparator_AegisRewards, keccak256(encodeClaimRequest));
      return toSign(signerPk, dataHash);
    }

    function toSign(uint256 signerPk, bytes32 data) public view returns(bytes memory signature) {
      (uint8 v ,bytes32 r, bytes32 s) = vm.sign(signerPk, data);
      signature = abi.encodePacked(r, s, v);
    }


    function _mint(address _user, uint256 _assetAmount, uint256 _yusdAmount, uint256 _nonce) public {
      vm.prank(owner);
      aegisConfig.whitelistAddress(_user, true);
      assetToken.mint(_user, _assetAmount);

      vm.startPrank(_user);
      OrderLib.Order memory order = OrderLib.Order({
          orderType: OrderLib.OrderType.MINT,
          userWallet: _user,
          collateralAsset: address(assetToken),
          collateralAmount: _assetAmount,
          yusdAmount: _yusdAmount,
          slippageAdjustedAmount: _yusdAmount,
          expiry: block.timestamp + 10000,
          nonce: _nonce,
          additionalData: ""
      });

      bytes memory signature = signOrder(trustedSignerAccount.privateKey, order);

      assetToken.approve(address(aegisMinting), 100e18);
      aegisMinting.mint(order, signature);
      vm.stopPrank();
    }

    function _requestRedeem(address _user, uint256 _assetAmount, uint256 _yusdAmount, string memory _requestId) internal {
      vm.startPrank(_user);

      OrderLib.Order memory redeemOrder = OrderLib.Order({
        orderType: OrderLib.OrderType.REDEEM,
        userWallet: _user,
        collateralAsset: address(assetToken),
        collateralAmount: _assetAmount,
        yusdAmount: _yusdAmount,
        slippageAdjustedAmount: _yusdAmount,
        expiry: block.timestamp + 10000,
        nonce: uint256(block.timestamp),
        additionalData: abi.encode(_requestId)
      });

      bytes memory signature = signOrder(trustedSignerAccount.privateKey, redeemOrder);

      yusd.approve(address(aegisMinting), _yusdAmount);
      aegisMinting.requestRedeem(redeemOrder, signature);
      vm.stopPrank();
    }

    function _approveRedeemRequest(string memory _requestId, uint256 _assetAmount) public {
      vm.prank(owner);
      aegisMinting.approveRedeemRequest(_requestId, _assetAmount);
    }

    function _stringToBytes32(string memory source) private pure returns (bytes32 result) {
      bytes memory str = bytes(source);
      if (str.length == 0) {
        return 0x0;
      }

      assembly {
        result := mload(add(source, 32))
      }
    }


    function testWeirdFeeRules() public {

      address alice = makeAddr("alice");

      uint256 assetAmount = 10e18;
      uint256 yusdAmount = 9.99e18;

      _mint(alice, assetAmount, yusdAmount, uint256(block.timestamp));

      for(uint256 i = 1; i <= 10; i++) {
        address _user = address(uint160(i)); // generate batch addresses
        string memory _requestId = string(abi.encodePacked(i));

        _mint(_user, assetAmount, yusdAmount, uint256(block.timestamp));
        assertEq(yusd.balanceOf(_user), yusdAmount);

        _requestRedeem(_user, assetAmount, yusdAmount, _requestId);

        // vm.prank(owner);
        // uint256 allBalance =  aegisMinting.custodyAvailableAssetBalance(address(assetToken));
        vm.prank(owner);
        aegisMinting.transferToCustody(custodian, address(assetToken), assetAmount);
        uint256 profit = assetAmount / 2;
        assetToken.mint(address(custodian), profit);
        vm.prank(custodian);
        assetToken.transfer(address(aegisMinting), assetAmount + profit);

        _approveRedeemRequest(_requestId, assetAmount);
      }
      vm.prank(owner);
      aegisMinting.setRedeemFeeBP(5000);
      // assertEq(aegisMinting.mintFeeBP(), 0, "mintFeeBP");
      assertEq(aegisMinting.redeemFeeBP(), 5000, "redeemFeeBP");


      assertEq(assetToken.balanceOf(address(aegisMinting)), 50e18 + 10e18);
      assertEq(assetToken.balanceOf(address(custodian)), 0);

      assertEq(yusd.balanceOf(address(alice)), 9.99e18);

      _requestRedeem(alice, assetAmount, yusdAmount, "alice-request-id");
      _approveRedeemRequest("alice-request-id", 10e18);

      assertEq(assetToken.balanceOf(address(alice)), 10e18);


      assertEq(yusd.balanceOf(address(aegisMinting)), 0);
      assert(yusd.totalSupply() == 4.995e18 && yusd.totalSupply() == yusd.balanceOf(address(insuranceFund)));
    }

}
```

### Mitigation

The fee calculation logic in `approveRedeemRequest` should be adjusted so that the fee is correctly charged to the redeeming user. This will ensure that the protocol's asset-liability accounting remains accurate and prevent future users from being unfairly burdened with fees.