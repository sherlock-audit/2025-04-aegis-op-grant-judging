Proper Taffy Mantaray

Medium

# Mint/redeem limit should check for the actual minted YUSD amount


### Summary

Mint/redeem limit should check for the actual minted YUSD amount

### Root Cause

Take mint limit as an example. It adds the `order.yusdAmount` to the mint limit, when in fact, the actual YUSD mint amount may be smaller.

The actual YUSD mint amount depends on the collateral price at the time of the transaction. For example, collateral amount = 100, collateral price = 20, then YUSD = 2000. If 2000 is smaller than `order.yusdAmount`, then only 2000 YUSD is minted.

In this case, we should only add 2000 YUSD to the mint limit, and not the original `order.yusdAmount`.

https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L239

```solidity
  function mint(
    OrderLib.Order calldata order,
    bytes calldata signature
  ) external nonReentrant onlyWhitelisted(order.userWallet) onlySupportedAsset(order.collateralAsset) {
    ...
@>  _checkMintRedeemLimit(mintLimit, order.yusdAmount);
    ...

@>  uint256 yusdAmount = _calculateMinYUSDAmount(order.collateralAsset, order.collateralAmount, order.yusdAmount);
    if (yusdAmount < order.slippageAdjustedAmount) {
      revert PriceSlippage();
    }
    ...
  }

  function _calculateMinYUSDAmount(address collateralAsset, uint256 collateralAmount, uint256 yusdAmount) internal view returns (uint256) {
    (uint256 chainlinkPrice, uint8 feedDecimals) = _getAssetUSDPriceChainlink(collateralAsset);
    if (chainlinkPrice == 0) {
      return yusdAmount;
    }

    uint256 chainlinkYUSDAmount = Math.mulDiv(
      collateralAmount * 10 ** (18 - IERC20Metadata(collateralAsset).decimals()),
      chainlinkPrice,
      10 ** feedDecimals
    );

    // Return smallest amount
    return Math.min(yusdAmount, chainlinkYUSDAmount);
  }

  function _checkMintRedeemLimit(MintRedeemLimit storage limits, uint256 yusdAmount) internal {
    if (limits.periodDuration == 0 || limits.maxPeriodAmount == 0) {
      return;
    }
    uint256 currentPeriodEndTime = limits.currentPeriodStartTime + limits.periodDuration;
    if (
      (currentPeriodEndTime >= block.timestamp && limits.currentPeriodTotalAmount + yusdAmount > limits.maxPeriodAmount) ||
      (currentPeriodEndTime < block.timestamp && yusdAmount > limits.maxPeriodAmount)
    ) {
      revert LimitReached();
    }
    // Start new mint period
    if (currentPeriodEndTime <= block.timestamp) {
      limits.currentPeriodStartTime = uint32(block.timestamp);
      limits.currentPeriodTotalAmount = 0;
    }

    limits.currentPeriodTotalAmount += yusdAmount;
  }

```

### Internal pre-conditions

`order.yusdAmount` is larger than collateral value in USD at the time of transaction.

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

We may add more YUSD to the limit than expected. This will affect other users to mint less YUSD in the same period of time.

### PoC

N/A

### Mitigation

Add the actual `yusdAmount` to mint/redeem limit.