Muscular Goldenrod Ape

Medium

# Method depositIncome() is missing slippage check

### Summary

Method `AegisMinting::depositIncome()` is used to issue YUSD rewards for excess collateral in the contract. This method takes the order input. The order input also contains `order.slippageAdjustedAmount`. Which is the min amount of YUSD tokens to be issued. But the below method `depositIncome()` doesn't check `order.slippageAdjustedAmount`. Since, the collateral and YUSD prices are collected from the oracle, the prices can vary and it can lead to the method missing less YUSD than expected if no `slippageAdjustedAmount` is applied.

```solidity
function depositIncome(
    OrderLib.Order calldata order,
    bytes calldata signature
  ) external nonReentrant onlyRole(FUNDS_MANAGER_ROLE) onlySupportedAsset(order.collateralAsset) {
    if (order.orderType != OrderLib.OrderType.DEPOSIT_INCOME) {
      revert InvalidOrder();
    }
    order.verify(getDomainSeparator(), aegisConfig.trustedSigner(), signature);
    _deduplicateOrder(order.userWallet, order.nonce);

    uint256 availableAssetFunds = _untrackedAvailableAssetBalance(order.collateralAsset);
    if (availableAssetFunds < order.collateralAmount) {
      revert NotEnoughFunds();
    }

@>    uint256 yusdAmount = _calculateMinYUSDAmount(order.collateralAsset, order.collateralAmount, order.yusdAmount);

    _custodyTransferrableAssetFunds[order.collateralAsset] += order.collateralAmount;

    // Transfer percent of YUSD rewards to insurance fund
    (uint256 mintAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(yusdAmount, incomeFeeBP);
    if (fee > 0) {
      yusd.mint(insuranceFundAddress, fee);
    }

    // Mint YUSD rewards to AegisRewards contract
    yusd.mint(address(aegisRewards), mintAmount);
    aegisRewards.depositRewards(order.additionalData, mintAmount);

    emit DepositIncome(
      abi.decode(order.additionalData, (string)),
      _msgSender(),
      order.collateralAsset,
      order.collateralAmount,
      mintAmount,
      fee,
      block.timestamp
    );
  }
```

### Root Cause

The issue lies in the below mentioned code. There is `yusdAmount >= order.slippageAdjustedAmount` check missing.

- https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L397C3-L435C4

### Internal Pre-conditions

-

### External Pre-conditions

-

### Attack Path

1. There are 100 USD of untracked USD in the contract.
2. Protocol calls `depositIncome()` and expects 100 YUSD.
3. Protocol receives 94 YUSD from conversion.

### Impact

YUSD minted by `depositIncome()` is distributed to the users. So, users will get less rewards.

### PoC

_No response_

### Mitigation

Add the slippage check as below:

```diff
function depositIncome(
    OrderLib.Order calldata order,
    bytes calldata signature
  ) external nonReentrant onlyRole(FUNDS_MANAGER_ROLE) onlySupportedAsset(order.collateralAsset) {
    if (order.orderType != OrderLib.OrderType.DEPOSIT_INCOME) {
      revert InvalidOrder();
    }
    order.verify(getDomainSeparator(), aegisConfig.trustedSigner(), signature);
    _deduplicateOrder(order.userWallet, order.nonce);

    uint256 availableAssetFunds = _untrackedAvailableAssetBalance(order.collateralAsset);
    if (availableAssetFunds < order.collateralAmount) {
      revert NotEnoughFunds();
    }

    uint256 yusdAmount = _calculateMinYUSDAmount(order.collateralAsset, order.collateralAmount, order.yusdAmount);

+   if (yusdAmount < order.slippageAdjustedAmount) {
+     revert PriceSlippage();
+   }

    _custodyTransferrableAssetFunds[order.collateralAsset] += order.collateralAmount;

    // Transfer percent of YUSD rewards to insurance fund
    (uint256 mintAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(yusdAmount, incomeFeeBP);

    if (fee > 0) {
      yusd.mint(insuranceFundAddress, fee);
    }

    // Mint YUSD rewards to AegisRewards contract
    yusd.mint(address(aegisRewards), mintAmount);
    aegisRewards.depositRewards(order.additionalData, mintAmount);

    emit DepositIncome(
      abi.decode(order.additionalData, (string)),
      _msgSender(),
      order.collateralAsset,
      order.collateralAmount,
      mintAmount,
      fee,
      block.timestamp
    );
  }
```