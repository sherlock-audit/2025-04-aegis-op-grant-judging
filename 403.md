Old Cyan Crocodile

Medium

# Double accounting of frozen funds in `AegisMinting._untrackedAvailableAssetBalance`  will DoS `approveRedeemRequest()` and `depositIncome()`

### Summary

A logic error in `_untrackedAvailableAssetBalance()` will cause a denial of service for redemption and income deposit operations because of an accounting error that double-counts frozen funds, artificially reducing the calculated available balance and preventing legitimate operations.

### Root Cause

In [`AegisMinting.sol:716-724`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L716-L723), the implementation of `_untrackedAvailableAssetBalance()` incorrectly accounts frozen funds twice from the available balance in both `if (balance < _custodyTransferrableAssetFunds[_asset] + assetFrozenFunds[_asset]) {return 0;}` and `return balance - _custodyTransferrableAssetFunds[_asset] - assetFrozenFunds[_asset];` as we can see here:

```solidity
function _untrackedAvailableAssetBalance(address _asset) internal view returns (uint256) {
  uint256 balance = IERC20(_asset).balanceOf(address(this));
  if (balance < _custodyTransferrableAssetFunds[_asset] + assetFrozenFunds[_asset]) {
    return 0;
  }

  return balance - _custodyTransferrableAssetFunds[_asset] - assetFrozenFunds[_asset];
}
```

The issue is that `assetFrozenFunds[_asset]` is a portion of funds which will always be included in `_custodyTransferrableAssetFunds[_asset]` as indicated in the comment at the `assetFrozenFunds` declaration:

```solidity
/// @dev Asset funds that were frozen and cannot be transfered to custody
```

This is further confirmed by the implementation of `_custodyAvailableAssetBalance`, which correctly treats frozen funds as a subset of transferrable funds:

```solidity
function _custodyAvailableAssetBalance(address _asset) internal view returns (uint256) {
  uint256 custodyTransferrableFunds = _custodyTransferrableAssetFunds[_asset];
  uint256 balance = IERC20(_asset).balanceOf(address(this));
  if (balance < custodyTransferrableFunds || custodyTransferrableFunds < assetFrozenFunds[_asset]) {
    return 0;
  }

  return custodyTransferrableFunds - assetFrozenFunds[_asset];
}
```

We also see a mention of "dirty funds" which we can assume are newly transferred collateral into the protocol through minting fresh YUSD and not funds transfered from the custodian:

```solidity
  /// @dev Returns custody transferrable asset funds minus durty funds
  function custodyAvailableAssetBalance(address asset) public view returns (uint256) {
    return _custodyAvailableAssetBalance(asset);
  }

  /// @dev Returns asset balance minus custody transferrable and durty funds
  function untrackedAvailableAssetBalance(address asset) public view returns (uint256) {
    return _untrackedAvailableAssetBalance(asset);
  }
```

This tells us this has to be minted collateral that came from a questionable wallet for whatever reason and this funds were frozen instead of being transferred to the custodian using either of these calls `transferToCustody()` or `forceTransferToCustody()` we can then conclude that  `assetFrozenFunds[_asset]` will always be a part of `_custodyTransferrableAssetFunds[asset` as it was added during `mint()` or `depositIncome`.

### Internal Pre-conditions

1. Some user collateral has been deposited via mint(), which increased `_custodyTransferrableAssetFunds[asset]`.
2. Funds calls freezeFunds() to freeze the deposited collateral, which increases assetFrozenFunds[asset] without decreasing _custodyTransferrableAssetFunds[asset]
3. A redemption request has been submitted via requestRedeem() or funds manager is attempting to process income via depositIncome()

### External Pre-conditions

1. The funds manager finds the source of the collaterall questionable or any valid reason to freeze collateral.
2. Income deposited needs to be frozen for whatever reason (The docs do not mention rationalle for freezing collateral.)

### Vulnerability Path

1. Users deposit collateral Asset A via `mint()`, which sets `_custodyTransferrableAssetFunds[Asset A] = X`.
2. The funds manager freezes these funds by calling `freezeFunds(Asset A, Y)` where Y â‰¤ X, setting `assetFrozenFunds[Asset A] = Y`.
3. Total contract balance of Asset A is B (which includes both tracked and untracked funds).
4. When a user's redemption is being processed via `approveRedeemRequest()`, the function calls `_untrackedAvailableAssetBalance(Asset A)` to check available funds.
5. This function incorrectly calculates:
 - `availableFunds = B - X - Y`
 - But the correct calculation should be `availableFunds = B - X` since Y is already part of X.
6. Similarly, when a funds manager calls `depositIncome()`, it also uses the same function with the same issue.
7. The incorrect calculation artificially reduces the calculated available balance by Y
8. If the actual available untracked funds are between (B-X-Y) and (B-X), then the operation will incorrectly revert with `NotEnoughFunds()`

Detailed accounting example:
- Contract has 100 Asset A tokens total (B = 100)
- 60 tokens are tracked for custody transfer (X = 60)
- 20 tokens from that 60 are frozen (Y = 20)
- Actual untracked funds available = 100 - 60 = 40 tokens
- Incorrectly calculated available funds = 100 - 60 - 20 = 20 tokens
- If a redemption or income deposit requires between 21 and 40 tokens, it will incorrectly fail

### Impact

Users and the protocol cannot execute critical operations, specifically:
1. Users cannot redeem their YUSD for collateral via approveRedeemRequest() even when sufficient collateral exists in the contract
2. The protocol cannot process income via depositIncome() even when sufficient untracked collateral exists

This breaks core functionality of the protocol and disrupts operations, preventing users from exiting positions and the protocol from distributing rewards. The protocol's off-exchange settlement model is compromised as funds remain in the contract longer than necessary.

### PoC

See Vulnerability Path

### Mitigation

Modify the `_untrackedAvailableAssetBalance` function to correctly calculate the untracked funds without double-counting frozen amounts:

```diff
function _untrackedAvailableAssetBalance(address _asset) internal view returns (uint256) {
  uint256 balance = IERC20(_asset).balanceOf(address(this));
- if (balance < _custodyTransferrableAssetFunds[_asset] + assetFrozenFunds[_asset]) {
+ if (balance < _custodyTransferrableAssetFunds[_asset]) {
    return 0;
  }

- return balance - _custodyTransferrableAssetFunds[_asset] - assetFrozenFunds[_asset];
+ return balance - _custodyTransferrableAssetFunds[_asset];
}
```

Alternatively, if frozen funds should be considered entirely separate from transferrable funds, then the `freezeFunds` function should be updated to decrease `_custodyTransferrableAssetFunds` when funds are frozen:

```diff
function freezeFunds(address asset, uint256 amount) external onlyRole(FUNDS_MANAGER_ROLE) onlySupportedAsset(asset) {
  if (assetFrozenFunds[asset] + amount > IERC20(asset).balanceOf(address(this))) {
    revert InvalidAmount();
  }
  
+ // Ensure frozen funds are removed from transferrable pool
+ if (_custodyTransferrableAssetFunds[asset] < amount) {
+   revert InvalidAmount();
+ }
+ _custodyTransferrableAssetFunds[asset] -= amount;
  assetFrozenFunds[asset] += amount;

  emit FreezeFunds(asset, amount);
}
```