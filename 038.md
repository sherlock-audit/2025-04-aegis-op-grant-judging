Modern Dijon Viper

Medium

# Potential incorrect min collateral amount calculated in redeem flow

### Summary

The function `_getAssetYUSDPriceOracle()` returns value with inconsistent decimals, which can cause the calculation of min collateral incorrect

### Root Cause

The function [`_getAssetYUSDPriceOracle()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L817-L834) fetches yUSD/USD price from AegisOracle and asset/USD price from Chainlink. The price asset/yUSD is calculated based on these fetched prices, as `(assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice)`. It is obvious that the result will be in term of `assetUSDPrice`'s decimals, specifically the decimals of the asset/USD Chainlink feed. Besides, the second returned value is `yusdUSDPriceDecimals`, which is set as `8` from the function [`AegisOracle.decimals()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisOracle.sol#L38-L40)

The returned value of the function `_getAssetYUSDPriceOracle()` is then used in the function [`_calculateRedeemMinCollateralAmount()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L751-L783) to calculate the collateral amount using that asset/yUSD price `(uint256 oraclePrice, uint8 oracleDecimals) = _getAssetYUSDPriceOracle(collateralAsset)`. Here, it is inconsistent that the `oraclePrice` is not in term of decimals `oracleDecimals` (as mentioned above, `oraclePrice` is in term of the asset/USD Chainlink feed decimals and `oracleDecimals` is `8`). This can cause the calculation of `oracleCollateralAmount` to be incorrect in case the asset/USD Chainlink feed decimals is different than `8`. 

For example, assume the asset/USD feed has decimals `18` and asset token has decimals `8` then `oracleCollateralAmount = yusdAmount * (10 ** oracleDecimals) / oraclePrice * 10 ** (18 - assetTokenDecimals)` will result a value having decimals different from the asset token decimals. This causes `collateralAmount = Math.min(collateralAmount, oracleCollateralAmount)` returns unexpected result because `minCollateralAmount` and `oracleCollateralAmount` have different decimals

```solidity
  function _getAssetYUSDPriceOracle(address asset) internal view returns (uint256, uint8) {
    if (address(aegisOracle) == address(0)) {
      return (0, 0);
    }

    int256 yusdUSDPrice = aegisOracle.yusdUSDPrice();
    if (yusdUSDPrice == 0) {
      return (0, 0);
    }
    uint8 yusdUSDPriceDecimals = aegisOracle.decimals();
    (uint256 assetUSDPrice, ) = _getAssetUSDPriceChainlink(asset);

    return ((assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice), yusdUSDPriceDecimals); /// <<<<< same decimals as asset/USD price feed's decimals
  }

  function _calculateRedeemMinCollateralAmount(
    address collateralAsset,
    uint256 collateralAmount,
    uint256 yusdAmount
  ) internal view returns (uint256) {
    // Calculate collateral amount for chainlink asset price.
    (uint256 chainlinkPrice, uint8 feedDecimals) = _getAssetUSDPriceChainlink(collateralAsset);
    if (chainlinkPrice > 0) {
      uint256 chainlinkCollateralAmount = Math.mulDiv(
        yusdAmount,
        10 ** feedDecimals,
        chainlinkPrice * 10 ** (18 - IERC20Metadata(collateralAsset).decimals())
      );

      // Get smallest amount
      collateralAmount = Math.min(collateralAmount, chainlinkCollateralAmount);
    }

    // Calculate collateral amount for aegisOracle asset/YUSD price.
    (uint256 oraclePrice, uint8 oracleDecimals) = _getAssetYUSDPriceOracle(collateralAsset);
    if (oraclePrice > 0) {
      uint256 oracleCollateralAmount = Math.mulDiv(
        yusdAmount,
        10 ** oracleDecimals,
        oraclePrice * 10 ** (18 - IERC20Metadata(collateralAsset).decimals())
      );

      // Get smallest amount
      collateralAmount = Math.min(collateralAmount, oracleCollateralAmount);
    }

    return collateralAmount;
  }
```

### Internal Pre-conditions

NA

### External Pre-conditions

Collateral asset with Chainlink feed decimals different than `8`

### Attack Path

Described above

### Impact

- Unexpected min collateral amount calculated in redeem flow

### PoC

_No response_

### Mitigation

```diff
  function _getAssetYUSDPriceOracle(address asset) internal view returns (uint256, uint8) {
    if (address(aegisOracle) == address(0)) {
      return (0, 0);
    }

    int256 yusdUSDPrice = aegisOracle.yusdUSDPrice();
    if (yusdUSDPrice == 0) {
      return (0, 0);
    }
    uint8 yusdUSDPriceDecimals = aegisOracle.decimals();
-    (uint256 assetUSDPrice, ) = _getAssetUSDPriceChainlink(asset);
+    (uint256 assetUSDPrice, uint8 feedDecimals) = _getAssetUSDPriceChainlink(asset);

-    return ((assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice), yusdUSDPriceDecimals);
+    return ((assetUSDPrice * 10 ** yusdUSDPriceDecimals) / uint256(yusdUSDPrice), feedDecimals);
  }
```