Straight Raspberry Griffin

Medium

# Redeem fee is not actually charged to the redeeming user

### Summary

When fulfilling a redeem request via approveRedeemRequest(), the protocol can take a fee. This fee is subtracted from the total YUSD the user is redeeming, however the user receives the full collateral amount regardless:

https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L315
```js
  function approveRedeemRequest(string calldata requestId, uint256 amount) external nonReentrant onlyRole(FUNDS_MANAGER_ROLE) whenRedeemUnpaused {
    RedeemRequest storage request = _redeemRequests[keccak256(abi.encode(requestId))];
    if (request.timestamp == 0 || request.status != RedeemRequestStatus.PENDING) {
      revert InvalidRedeemRequest();
    }
    if (amount == 0 || amount > request.order.collateralAmount) {
      revert InvalidAmount();
    }

    uint256 collateralAmount = _calculateRedeemMinCollateralAmount(request.order.collateralAsset, amount, request.order.yusdAmount);
    /*
     * Reject if:
     * - asset is no longer supported
     * - smallest amount is less than order minAmount
     * - order expired
     */
    if (
      !_supportedAssets.contains(request.order.collateralAsset) ||
      collateralAmount < request.order.slippageAdjustedAmount ||
      request.order.expiry < block.timestamp
    ) {
      _rejectRedeemRequest(requestId, request);
      return;
    }

    uint256 availableAssetFunds = _untrackedAvailableAssetBalance(request.order.collateralAsset);
    if (availableAssetFunds < collateralAmount) {
      revert NotEnoughFunds();
    }

    // @audit should take a proportional amount of assets as well, because this is taking the fee from all users.
    // Take a fee, if it's applicable
    (uint256 burnAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(request.order.yusdAmount, redeemFeeBP);
    if (fee > 0) {
      yusd.safeTransfer(insuranceFundAddress, fee);
    }

    request.status = RedeemRequestStatus.APPROVED;
    totalRedeemLockedYUSD -= request.order.yusdAmount;

    IERC20(request.order.collateralAsset).safeTransfer(request.order.userWallet, collateralAmount);
    yusd.burn(burnAmount);

    emit ApproveRedeemRequest(requestId, _msgSender(), request.order.userWallet, request.order.collateralAsset, collateralAmount, burnAmount, fee);
  }
```
So, the YUSD sent to the insuranceFundAddress does not have any backing collateral.
This has the effect of reducing the price of YUSD, since the total supply is decreased by less than the collateral value removed. Effectively taking the fee proportionally from all users, instead of just the redeeming user.

The correct behavior is to reduce the collateral amount the user is receiving as well by same value as the fee tokens.

### Root Cause

Proportional amount of collateral should be charged as fee during redeeming action.

### Internal Pre-conditions

A user withdrawing from the protocol.

### External Pre-conditions

No external preconditions

### Attack Path

Consider this scenario:

Assume no minting fee.  Redeem Fee is 5%. USDC only collateral for simplicity.

1. Users Alice and Mallory both have deposited 100e18 USDC and as a result have each have 100e18 YUSD
2. Mallory redeems her full deposit for 100e18 YUSD, 95e18YUSD gets burned, and 5e18 YUSD gets sent to the insurance fund address.

As a result of this there is now 100e6 USDC as collateral and the total supply of YUSD is 105e18, so actually Alice is the one charged for Mallorys redeem.

This can have different effects depending on how the AegisOracle is set up:

1. If there is no oracle, then the price of future redemptions will also be calculated as 1:1 peg with USD and the last user will not be able to withdraw.
E.g. in the above example, if the insurance fund wallet 5e18 YUSD is redeemed for 5e18 USDC, then Alice will not be able to redeem her whole 100e18 YUSD, only 95e18. 5e18 YUSD will become unredeemable.

2. If there is an AegisOracle set, the effect of this will depend on how the oracle reports the price. If the price is reduced(depegged from USD) then the same effect will happen, only fee loss will be distributed to all users equally, instead of just the last one to redeem.

### Impact

- Value of YUSD is depegged from USD due to not enough collateral present
- All users are charged a redeem fee whenever someone withdraws

### PoC

_No response_

### Mitigation

_No response_