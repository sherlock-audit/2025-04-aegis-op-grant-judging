Clean Aquamarine Haddock

High

# Wrong accounting of `_untrackedAvailableAssetBalance` the reward and redeem system is broken

### Summary

The function `AegisMinting::_untrackedAvailableAssetBalance` is the function responsible for providing the untracked balance which will be used to issue rewards and complete redeem process. But the accounting is incorrect in the function of freezing funds which could cause the complete DOS of the both function.

### Root Cause

The system has mechanism of freeze funds, the funds can be freeze so that it wouldn't be transferable to custodians or user. The funds which can be freeze is the fund which is available to transffered to custody can be confirm from here. 
```solidity
// AegisMinting::_custodyAvailableAssetBalance
  function _custodyAvailableAssetBalance(address _asset) internal view returns (uint256) {
    uint256 custodyTransferrableFunds = _custodyTransferrableAssetFunds[_asset];
    uint256 balance = IERC20(_asset).balanceOf(address(this));
    if (balance < custodyTransferrableFunds || custodyTransferrableFunds < assetFrozenFunds[_asset]) {
      return 0;
    }

@>    return custodyTransferrableFunds - assetFrozenFunds[_asset];
  }
```

The issue lies in in the `AegisMinting::freezeFunds` [check here](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/4aceb235db96b2299bb95ebf16e83a24f987bf3e/aegis-contracts/contracts/AegisMinting.sol#L605)
```solidity
 function freezeFunds(address asset, uint256 amount) external onlyRole(FUNDS_MANAGER_ROLE) onlySupportedAsset(asset) {
    if (assetFrozenFunds[asset] + amount > IERC20(asset).balanceOf(address(this))) {
      revert InvalidAmount();
    }

@>    assetFrozenFunds[asset] += amount;

    emit FreezeFunds(asset, amount);
  }
```
Here we are adding the `amount` in mapping `assetFrozenFunds[asset]`, but we are not resetting or decreasing this amount from 
`_custodyTransferrableAssetFunds[_asset]`, which cause the problem the way we fetching values in `_untrackedAvailableAssetBalance`


```solidity
  function _untrackedAvailableAssetBalance(address _asset) internal view returns (uint256) {
    uint256 balance = IERC20(_asset).balanceOf(address(this));
    if (balance < _custodyTransferrableAssetFunds[_asset] + assetFrozenFunds[_asset]) {
      return 0;
    }

@>    return balance - _custodyTransferrableAssetFunds[_asset] - assetFrozenFunds[_asset];
  }
```
Here we decreasing the value 
```solidity
balance - _custodyTransferrableAssetFunds[_asset] - assetFrozenFunds[_asset];
```
But as `assetFrozenFunds[_asset]` this value already exist in this `_custodyTransferrableAssetFunds[_asset]`. So this amount `assetFrozenFunds[_asset]` has been subtracted twice here which shouldn't happen.



### Internal Pre-conditions

1) FUND MANAGER have freeze the fund

### External Pre-conditions

...

### Attack Path

No attack, Explaining the theory of POC
1) User use 100 USDC to mint 100 YUSD.
2) Now AegisMiniting has balance of 100 USDC.
3) Fund Manager has freeze the 50 USDC.
3) Rewards came of 20 USDC sent before calling the function `AegisMinting::depositIncome` 
4) AegisMinting have balance of 120 UDSC, (50 USDC freezed). Still the `AegisMinting::depositIncome` revert because of wrong implementation

### Impact

1) `AegisMinting::depositIncome` is used to issue rewards to holders, it will always reverts when there is some value is freezed. Because`order.collateralAmount` will always be bigger then `availableAssetFunds`. So always revert (Complete DOS). Even though the money exist
```solidity
// AegisMinting::depositIncome
    uint256 availableAssetFunds = _untrackedAvailableAssetBalance(order.collateralAsset);
  @>  if (availableAssetFunds < order.collateralAmount) {
      revert NotEnoughFunds();
    }
```

2. `AegisMinting::approveRedeemRequest` is used by fund manager to complete the redeem in 2nd step for user, as explained above same issue `collateralAmount` will always be bigger then `availableAssetFunds` so it's always revert (Complete DOS). Even though the money exist
```solidity
// AegisMinting::approveRedeemRequest
    uint256 availableAssetFunds = _untrackedAvailableAssetBalance(request.order.collateralAsset);
    if (availableAssetFunds < collateralAmount) {
      revert NotEnoughFunds();
    }
```
 

### PoC

I have used foundry to write test, use this step for [integration](https://hardhat.org/hardhat-runner/docs/advanced/hardhat-and-foundry)
```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";
import {YUSD} from "contracts/YUSD.sol";
import {AegisMinting} from "contracts/AegisMinting.sol";
import {AegisConfig} from "contracts/AegisConfig.sol";
import {AegisOracle} from "contracts/AegisOracle.sol";
import {AegisRewards} from "contracts/AegisRewards.sol";

import {FeedRegistry} from "contracts/test/FeedRegistry.sol";
import {TestToken} from "contracts/test/TestToken.sol";

import {IAegisRewards} from "contracts/interfaces/IAegisRewards.sol";
import {IAegisMintingErrors} from "contracts/interfaces/IAegisMinting.sol";
import {IAegisConfig} from "contracts/interfaces/IAegisConfig.sol";
import {IAegisOracle} from "contracts/interfaces/IAegisOracle.sol";
import {IYUSD} from "contracts/interfaces/IYUSD.sol";
import {FeedRegistryInterface} from "@chainlink/contracts/src/v0.8/interfaces/FeedRegistryInterface.sol";
import {OrderLib} from  "contracts/lib/OrderLib.sol";
import {ClaimRewardsLib} from  "contracts/lib/ClaimRewardsLib.sol";


import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import "@openzeppelin/contracts/utils/cryptography/MessageHashUtils.sol";
import { Denominations } from "@chainlink/contracts/src/v0.8/Denominations.sol";

contract POC is Test {

    using ECDSA for bytes32;
    bytes32 internal constant ORDER_TYPE_HASH = keccak256(
        "Order(uint8 orderType,address userWallet,address collateralAsset,uint256 collateralAmount,uint256 yusdAmount,uint256 slippageAdjustedAmount,uint256 expiry,uint256 nonce,bytes additionalData)"
    );
    

    AegisMinting aeMint;
    IAegisConfig aegisConfig;
    AegisOracle aeOracle;
    AegisRewards aeRewards;

    YUSD yusd;
    TestToken mockERC20;
    FeedRegistry feedReg;

    address admin = makeAddr("admin");
    address oracle = makeAddr("oracle");
    address[] _assets;
    uint32[] heartbeat;
    address[] custodian;
    address[] ops;
    address _initialOwner = makeAddr("_initialOwner");
    address user = makeAddr("user");
    address _custodian = makeAddr("_custodian");
    address wallet = makeAddr("wallet");

    bytes32 private constant SETTINGS_MANAGER_ROLE = keccak256("SETTINGS_MANAGER_ROLE");
    bytes32 private constant FUNDS_MANAGER_ROLE = keccak256("FUNDS_MANAGER_ROLE");
    bytes32 private constant COLLATERAL_MANAGER_ROLE = keccak256("COLLATERAL_MANAGER_ROLE");
    bytes32 private constant REWARDS_MANAGER_ROLE = keccak256("REWARDS_MANAGER_ROLE");

    uint256[] amounts;
    bytes32[]  ids;





    uint256 demoPrivateKey = 0x8db9a2e420ea6bf45ebd46411be83ae7f66cab825e04769f5c3a60b264d36c04;
    address trustedSigner = vm.addr(demoPrivateKey);

      using ClaimRewardsLib for ClaimRewardsLib.ClaimRequest;


    function setUp() public {
        // Correctly push into dynamic arrays

        mockERC20 = new TestToken("MOCK USDC", "USDC", 6);
        _assets.push(address(1));
        _assets.push(address(mockERC20));
        custodian.push(_custodian);

        ops.push(admin);

        heartbeat.push(5);
        heartbeat.push(0);

        // Optionally, if you want custodians as well, push addresses
        // custodian.push(address(4)); // Example if needed
        //   constructor(address _trustedSigner, address[] memory _ops, address _initialOwner) Ownable(_initialOwner) {
        
        yusd = new YUSD(admin);
        aeOracle = new AegisOracle(ops, _initialOwner);
        feedReg = new FeedRegistry();
        AegisConfig _aegisConfig = new AegisConfig(trustedSigner, ops, _initialOwner);


        IYUSD _yusd = IYUSD(address(yusd));
        aegisConfig = IAegisConfig(address(_aegisConfig));
        aeRewards = new AegisRewards(_yusd, aegisConfig, admin); 
        IAegisRewards _aegisRewards = IAegisRewards(address(2));
        IAegisOracle _aegisOracle = IAegisOracle(address(aeOracle));
        FeedRegistryInterface _fdRegistry = FeedRegistryInterface(address(feedReg));

        aeMint = new AegisMinting(
            _yusd,
            aegisConfig,
            _aegisRewards,
            _aegisOracle,
            _fdRegistry,
            address(0), 
            _assets,
            heartbeat,
            custodian,
            admin
        );

        // grant roles
        vm.startPrank(admin);
        aeMint.grantRole(SETTINGS_MANAGER_ROLE, admin);
        aeMint.grantRole(FUNDS_MANAGER_ROLE, admin);
        aeMint.grantRole(COLLATERAL_MANAGER_ROLE, admin);
        aeMint.setRedeemFeeBP(1000);

        _aegisConfig.whitelistAddress(user, true);
        aeRewards.grantRole(REWARDS_MANAGER_ROLE, admin);
        aeRewards.setAegisMintingAddress(address(aeMint));
        vm.stopPrank();

         vm.startPrank(_aegisConfig.owner());
            _aegisConfig.setTrustedSigner(trustedSigner);
         vm.stopPrank();

        vm.prank(yusd.owner());
        yusd.setMinter(address(aeMint));

    }
    
    function testWrongImplementationOfUntrakedBalance() public {
      mintYusdOrder(); // Mint YUSD so AeMint have usdc balance 100 usdc
      uint balance =  yusd.balanceOf(user);
      assert(100e6 == mockERC20.balanceOf(address(aeMint)));
      console.log("balance",balance);

      // Let freeze some funds
      vm.startPrank(admin); // 0xaA10a84CE7d9AE517a52c6d5cA153b369Af99ecF
        aeMint.freezeFunds(address(mockERC20), (mockERC20.balanceOf(address(aeMint))/2)); // half amount 50 USDC 50 e6
      vm.stopPrank();


    mockERC20.mint(address(aeMint), 20e6); // 20 usdc mint more for rewards 
    assert(120e6 == mockERC20.balanceOf(address(aeMint)));

    
         // preparing depositIncome order   
         OrderLib.Order memory order = buildOrder(
            2, // Redeem
            admin,
            address(mockERC20),
            20e6,
            20 ether,
            0,
            block.timestamp + 1 hours,
            1,
            abi.encode("Rewards Issued")
        );
        bytes memory signature = signOrder(order, aeMint.getDomainSeparator(), demoPrivateKey);

        
        vm.startPrank(admin); // it will always revert because of wrong implememtation or double subtraction
        vm.expectRevert(IAegisMintingErrors.NotEnoughFunds.selector);
        aeMint.depositIncome(order, signature);





    }

    function mintYusdOrder() public {
         OrderLib.Order memory order = buildOrder(
            0, // Redeem
            user,
            address(mockERC20),
            100e6,
            100 ether,
            0,
            block.timestamp + 1 hours,
            1,
            abi.encode("Redeem Request 1")
        );

        
        bytes memory signature = signOrder(order, aeMint.getDomainSeparator(), demoPrivateKey);

        // Mock prices
        feedReg.setPrice(address(mockERC20), Denominations.USD, 1e8);
        vm.mockCall(
            address(aeOracle), 
            abi.encodeWithSelector(AegisOracle.yusdUSDPrice.selector),
            abi.encode(int256(1e8))
        );

        deal(address(mockERC20), user, 100 ether);
        vm.startPrank(user);
        mockERC20.approve(address(aeMint), mockERC20.balanceOf(user));
        aeMint.mint(order, signature);
        vm.stopPrank();
        // yusd.balanceOf(user);
    }

      function buildOrder(
        uint8 orderType,
        address _user,
        address collateralAsset,
        uint256 collateralAmount,
        uint256 yusdAmount,
        uint256 slippageAdjustedAmount,
        uint256 expiry,
        uint256 nonce,
        bytes memory additionalData
    ) public pure returns (OrderLib.Order memory) {
        return OrderLib.Order({
            orderType: OrderLib.OrderType(orderType),
            userWallet: _user,
            collateralAsset: collateralAsset,
            collateralAmount: collateralAmount,
            yusdAmount: yusdAmount,
            slippageAdjustedAmount: slippageAdjustedAmount,
            expiry: expiry,
            nonce: nonce,
            additionalData: additionalData
        });
    }

    function signOrder(
        OrderLib.Order memory order,
        bytes32 domainSeparator,
        uint256 privateKey
    ) public pure returns (bytes memory) {
        bytes memory encoded = abi.encode(
            ORDER_TYPE_HASH,
            order.orderType,
            order.userWallet,
            order.collateralAsset,
            order.collateralAmount,
            order.yusdAmount,
            order.slippageAdjustedAmount,
            order.expiry,
            order.nonce,
            keccak256(order.additionalData)
        );

        bytes32 structHash = keccak256(encoded);
        bytes32 digest = MessageHashUtils.toTypedDataHash(domainSeparator, structHash);
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(privateKey, digest);
        return abi.encodePacked(r, s, v);
    }

}
```

### Mitigation

Consider decreasing the balance in freeze fund itself. Or do not subtract twice