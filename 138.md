Soaring Lemon Finch

High

# Faulty fee logic in `approveRedeemRequest()` can dilute YUSD value

### Summary
A whitelisted user can call [`AegisMinting#requestRedeem()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L277-L307) to initial a request to redeem YUSD for supported asset like USDC, USDT, DAI. 
Funds manager will approve the request by calling [`AegisMinting#approveRedeemRequest()`](https://github.com/sherlock-audit/2025-04-aegis-op-grant/blob/main/aegis-contracts/contracts/AegisMinting.sol#L315-L358). A portion of YUSD will be transferred to `insuranceFundAddress` as fee, the rest will be burned to redeem the specified collateral:
```solidity
  function approveRedeemRequest(string calldata requestId, uint256 amount) external nonReentrant onlyRole(FUNDS_MANAGER_ROLE) whenRedeemUnpaused {
    RedeemRequest storage request = _redeemRequests[keccak256(abi.encode(requestId))];
    if (request.timestamp == 0 || request.status != RedeemRequestStatus.PENDING) {
      revert InvalidRedeemRequest();
    }
    if (amount == 0 || amount > request.order.collateralAmount) {
      revert InvalidAmount();
    }

@>  uint256 collateralAmount = _calculateRedeemMinCollateralAmount(request.order.collateralAsset, amount, request.order.yusdAmount);
    /*
     * Reject if:
     * - asset is no longer supported
     * - smallest amount is less than order minAmount
     * - order expired
     */
    if (
      !_supportedAssets.contains(request.order.collateralAsset) ||
      collateralAmount < request.order.slippageAdjustedAmount ||
      request.order.expiry < block.timestamp
    ) {
      _rejectRedeemRequest(requestId, request);
      return;
    }

    uint256 availableAssetFunds = _untrackedAvailableAssetBalance(request.order.collateralAsset);
    if (availableAssetFunds < collateralAmount) {
      revert NotEnoughFunds();
    }

    // Take a fee, if it's applicable
    (uint256 burnAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(request.order.yusdAmount, redeemFeeBP);
    if (fee > 0) {
      yusd.safeTransfer(insuranceFundAddress, fee);
    }

    request.status = RedeemRequestStatus.APPROVED;
    totalRedeemLockedYUSD -= request.order.yusdAmount;

    IERC20(request.order.collateralAsset).safeTransfer(request.order.userWallet, collateralAmount);
    yusd.burn(burnAmount);

    emit ApproveRedeemRequest(requestId, _msgSender(), request.order.userWallet, request.order.collateralAsset, collateralAmount, burnAmount, fee);
  }
```
However, the redeeming fee was not deducted when calculating the amount of collateral, causing the protocol to pay the redeeming fee and dilute YUSD value.

The above issue can be explained as below: 
Prerequisites:
```math
\begin{align*}
1 USDC == 1 YUSD == 1 USD \\
fee_{mint} =  fee_{redeem} = 10\% 
\end{align*}
```
- Alice mints YUSD with 1000 USDC: Alice received 900 YUSD, 100 YUSD was transferred to `insuranceFundAddress`
- Bob mints YUSD with 1000 USDC: Bob received 900 YUSD, 100 YUSD was transferred to `insuranceFundAddress`

Now the protocol owns 2000 USDC, the total supply of YUSD is 2000. One YUSD is still worth one USDC.
- Alice redeem 900 YUSD for USDC. Once approved, 900 USDC will be transferred to Alice; 90 YUSD will be transferred to `insuranceFundAddress`; 810 YUSD will be burned. 

Now the protocol owns 1100 USDC, the total supply of YUSD is 1190. The real value of YUSD is far less than 1 USDC.

Consequently, later redeemers will either incur a loss (YUSD price is updated in time), or the protocol will lack funds for the final redemptions(YUSD price was not updated in time).

### Root Cause

The redeeming fee was not deducted properly when calculating the amount of collateral

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact
The value of YUSD will be diluted each time when a redeem request is approved.

### PoC

### Mitigation
The redeeming fee must be deducted before calculating the amount of collateral: 
```diff
  function approveRedeemRequest(string calldata requestId, uint256 amount) external nonReentrant onlyRole(FUNDS_MANAGER_ROLE) whenRedeemUnpaused {
    RedeemRequest storage request = _redeemRequests[keccak256(abi.encode(requestId))];
    if (request.timestamp == 0 || request.status != RedeemRequestStatus.PENDING) {
      revert InvalidRedeemRequest();
    }
    if (amount == 0 || amount > request.order.collateralAmount) {
      revert InvalidAmount();
    }

-   uint256 collateralAmount = _calculateRedeemMinCollateralAmount(request.order.collateralAsset, amount, request.order.yusdAmount);
+   (uint256 burnAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(request.order.yusdAmount, redeemFeeBP);
+   uint256 collateralAmount = _calculateRedeemMinCollateralAmount(request.order.collateralAsset, amount, burnAmount);
    /*
     * Reject if:
     * - asset is no longer supported
     * - smallest amount is less than order minAmount
     * - order expired
     */
    if (
      !_supportedAssets.contains(request.order.collateralAsset) ||
      collateralAmount < request.order.slippageAdjustedAmount ||
      request.order.expiry < block.timestamp
    ) {
      _rejectRedeemRequest(requestId, request);
      return;
    }

    uint256 availableAssetFunds = _untrackedAvailableAssetBalance(request.order.collateralAsset);
    if (availableAssetFunds < collateralAmount) {
      revert NotEnoughFunds();
    }

    // Take a fee, if it's applicable
-   (uint256 burnAmount, uint256 fee) = _calculateInsuranceFundFeeFromAmount(request.order.yusdAmount, redeemFeeBP);
    if (fee > 0) {
      yusd.safeTransfer(insuranceFundAddress, fee);
    }

    request.status = RedeemRequestStatus.APPROVED;
    totalRedeemLockedYUSD -= request.order.yusdAmount;

    IERC20(request.order.collateralAsset).safeTransfer(request.order.userWallet, collateralAmount);
    yusd.burn(burnAmount);

    emit ApproveRedeemRequest(requestId, _msgSender(), request.order.userWallet, request.order.collateralAsset, collateralAmount, burnAmount, fee);
  }
```